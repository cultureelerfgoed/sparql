prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rn: <https://data.cultureelerfgoed.nl/term/id/rn/>
prefix rnce: <https://data.cultureelerfgoed.nl/id/rnce#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select ?shape ?shapeColor ?shapeLabel {
  graph graph:instanties-rce {
    ?rijksmonument
      ceo:datumInschrijvingInMonumentenregister ?datumInschrijving;
      ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam ?uri;
      ceo:rijksmonumentnummer ?rijksmonumentnummer;
      ceo:heeftBasisregistratieRelatie/ceo:heeftProvincie ?prov.
    filter not exists { ?rijksmonument ceo:heeftJuridischeStatus rn:3e79bb7c-b459-4998-a9ed-78d91d069227. }
    optional { ?rijksmonument ceo:heeftGeometrie/geo:asWKT ?shape. }
    bind(year(xsd:dateTime(?datumInschrijving)) as ?jaarInschrijving)
    filter(?jaarInschrijving >= ?beginJaarInschrijving && ?jaarInschrijving <= ?eindJaarInschrijving)
  }
  graph graph:owms { ?prov skos:prefLabel ?provincie. }  
  graph graph:bebouwdeomgeving {
    rn:1eeb48df-adbb-44b2-bcf1-33e3fe902413 skos:narrower ?narrow.
    ?narrow skos:prefLabel ?label.
  }
  values (?label ?narrow) { ('Religieuze gebouwen' rn:25fac0f1-77a2-4587-ab04-dfcb66959dd8) }
  ?narrow skos:narrower+ ?uri.
  ?uri skos:prefLabel ?uriSub.
  bind(replace(?uriSub,'\\s\\(.*\\)|\\(.*\\)','') as ?uriSubs)
  graph graph:denominatie {
    ?rijksmonument ceox:heeftDenominatie/ceox:heeftDenominatieNaam
      [ skos:prefLabel ?denominatie;
        rnce:hasChurchMovement/skos:prefLabel ?stroming ].
  }
  bind(strdt(concat('<p>',?uriSubs,' - ',?denominatie,' - ',?jaarInschrijving,' - rijksmonument <a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/',uri(?rijksmonumentnummer),'" target="_blank">',str(?rijksmonumentnummer),'</a></p>'),rdf:HTML) as ?shapeLabel)
  bind(if(?stroming='Christendom, Evangelisch','red',
       if(?stroming='Christendom, Katholiek','purple',
       if(?stroming='Christendom, Oecumenisch','maroon',
       if(?stroming='Christendom, Overig','black',
       if(?stroming='Christendom, Protestants','orange',
       if(?stroming='Christendom, Reformatorisch','gray',
       if(?stroming='Christendom, Vrijzinnig','white',
       if(?stroming='Jodendom','green',
       'fuchsia')))))))) as ?shapeColor)
}
