PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
SELECT ?jaarInschrijvings ?aantal  ?CumulatiefAantal
WHERE {
  GRAPH graph:instanties-rce {
  {
    select ?jaarInschrijving (COUNT(?rijksmonument) AS ?aantal) where {
      ?rijksmonument a ceo:Rijksmonument .
      ?rijksmonument ceo:datumInschrijvingInMonumentenregister ?datumInschrijving.
      MINUS {?rijksmonument ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/3e79bb7c-b459-4998-a9ed-78d91d069227>}
      BIND(year(xsd:dateTime(?datumInschrijving)) as ?jaarInschrijving)
      FILTER (?jaarInschrijving >= 1965 && ?jaarInschrijving <= 2022)
    } GROUP BY ?jaarInschrijving
  }  
  {
    select ?jaarInschrijving (COUNT(?rijksmonument) AS ?CumulatiefAantal) where {
      ?rijksmonument a ceo:Rijksmonument .
      ?rijksmonument ceo:datumInschrijvingInMonumentenregister ?datumInschrijving.
      MINUS {?rijksmonument ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/3e79bb7c-b459-4998-a9ed-78d91d069227>}
      BIND(year(xsd:dateTime(?datumInschrijving)) as ?jaarInschrijvingCounter)
      {
        select distinct ?jaarInschrijving where {
          ?rijksmonument a ceo:Rijksmonument .
          ?rijksmonument ceo:datumInschrijvingInMonumentenregister ?datumInschrijving.
          MINUS {?rijksmonument ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/3e79bb7c-b459-4998-a9ed-78d91d069227>}
          BIND(year(xsd:dateTime(?datumInschrijving)) as ?jaarInschrijving)
        }
        }
      FILTER (?jaarInschrijvingCounter <= ?jaarInschrijving)
    }  group by ?jaarInschrijving
    }
  }              bind(str(?jaarInschrijving) as ?jaarInschrijvings)
}
order by  ?jaarInschrijving