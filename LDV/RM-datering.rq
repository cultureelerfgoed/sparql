#+ name: RM-datering
#+ description: Datering van een Rijksmonument.
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

prefix ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rn: <https://data.cultureelerfgoed.nl/term/id/rn/>

select ?rijksmonumentnummer ?gebeurtenisNaam ?beginDatum ?eindDatum ?indicatienauwkeurigheid

where
{
  graph graph:instanties-rce {
    ?rijksmonument ceo:rijksmonumentnummer ?rijksmonumentnummer.
    minus {
      ?rijksmonument ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/3e79bb7c-b459-4998-a9ed-78d91d069227>
    }
    ?rijksmonument ceo:heeftGebeurtenis ?gebeurtenis.
    ?gebeurtenis ceo:heeftDatering ?datering.
    ?rijksmonument ceo:heeftGebeurtenis/ceo:heeftGebeurtenisNaam/skos:prefLabel ?gebeurtenisNaam.
    optional { ?gebeurtenis ceo:heeftDatering/ceo:heeftIndicatieNauwkeurigheid/skos:prefLabel ?indicatienauwkeurigheid. }
    optional {
      ?datering ceo:heeftBeginDatering ?beginDatering.
      ?beginDatering ceo:datum ?beginDatum.
    }
    optional {
      ?datering ceo:heeftEindDatering ?eindDatering.
      ?eindDatering ceo:datum ?eindDatum.
    }
  }
}
limit 500