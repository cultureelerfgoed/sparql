#+ name: RM-datering
#+ description: Rijksmonument datering
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

PREFIX ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rn: <https://data.cultureelerfgoed.nl/term/id/rn/>

SELECT ?rijksmonumentnummer ?naam ?woonplaatsnaam ?beginDatum ?eindDatum
       (GROUP_CONCAT(DISTINCT ?adres; SEPARATOR=" ::: ") AS ?adressen)

WHERE 
{
  GRAPH graph:instanties-rce {
    ?rijksmonument ceo:rijksmonumentnummer ?rijksmonumentnummer.
    MINUS { ?rijksmonument ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/3e79bb7c-b459-4998-a9ed-78d91d069227> }
    OPTIONAL { ?rijksmonument ceo:heeftNaam/ceo:naam ?naam. }
    OPTIONAL { ?rijksmonument ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:volledigAdres ?adres. }
    OPTIONAL { ?rijksmonument ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:woonplaatsnaam ?woonplaatsnaam. }
    OPTIONAL { 
      ?rijksmonument ceo:heeftGebeurtenis ?gebeurtenis.
      ?gebeurtenis ceo:heeftDatering ?datering.
      OPTIONAL {
        ?datering ceo:heeftBeginDatering ?beginDatering.
        ?beginDatering ceo:datum ?beginDatum.
      }
      OPTIONAL {
        ?datering ceo:heeftEindDatering ?eindDatering.
        ?eindDatering ceo:datum ?eindDatum.
      }
    }
  }
}
GROUP BY ?rijksmonumentnummer ?naam ?woonplaatsnaam ?beginDatum ?eindDatum
LIMIT 50