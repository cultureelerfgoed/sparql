#+ name: Query-8-2
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX gsp: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?ge ?rm ?gezichtnummer ?gezichtsnaam ?rmn ?oorspronkelijkeFunctie ?geogez ?geormn ('red' as ?geogezColor) ('blue' as ?geormColor)
WHERE {
  GRAPH graph:instanties-rce {
    ?ge a ceo:Gezicht ;
        ceo:gezichtsnummer ?gezichtnummer ;
        ceo:heeftNaam/ceo:naam ?gezichtsnaam ;
        ceo:heeftGeometrie/gsp:asWKT ?geogez .
    
    ?rm a ceo:Rijksmonument ;
        ceo:rijksmonumentnummer ?rmn ;
        ceo:heeftGeometrie/gsp:asWKT ?geormn .
    
    OPTIONAL { ?rm ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam ?functie }
    
    FILTER(geof:sfWithin(?geormn, ?geogez))
  }

  GRAPH graph:bebouwdeomgeving {
    OPTIONAL { ?functie skos:broader/skos:prefLabel ?oorspronkelijkeFunctie }
  }
}
ORDER BY ?gezichtnummer ?rmn
