#+ name: diff-cho-plus-min
#+ description: Samenvatting van de meest recente diff (toegevoegd en verwijderd)
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/diff-cho/sparql

PREFIX dct:  <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>

SELECT (COUNT(*) AS ?aantalTriples)
WHERE {
  {
    SELECT ?diffGraph
    WHERE {
      GRAPH <https://linkeddata.cultureelerfgoed.nl/rce/diff-cho/graph/index> {
        ?diffGraph dct:date ?datum ;
                   prov:used ?snapshot .
      }
    }
    ORDER BY DESC(?datum)
    LIMIT 1
  }

  GRAPH ?diffGraph {
    ?s ?p ?o .
  }
}
