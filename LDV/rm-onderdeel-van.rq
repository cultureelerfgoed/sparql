#+ name: rm-onderdeel-van
#+ description: Vereenvoudigd zodat geo data niet teruggegeven wordt
#- 
#- - 531019 Complex
#- - 531015 Hoofdobject Complex
#- - 18329 Werelderfgoed 
#- - 40035 Gezicht 
#- - 46869 Complex & Werelderfgoed
#- - 15669 Gezicht & Werelderfgoed
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

# Definieer prefixen
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix geof: <http://www.opengis.net/def/function/geosparql/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix units: <https://eulersharp.sourceforge.net/2003/03swap/units#>
prefix uom: <http://www.opengis.net/def/uom/OGC/1.0/>

# Selecteer de gewenste variabelen
select ?rijksmonument ?rijksmonumentnummer ?complex ?complexnummer  ?type ?werelderfgoed ?werelderfgoednummer ?gezicht ?gezichtnummer

where {
  # Specificeer de grafiek waarin gezocht wordt
  graph graph:instanties-rce {
    # Definieer een rijksmonument en zijn eigenschappen
    ?rijksmonument a ceo:Rijksmonument ;
                   ceo:rijksmonumentnummer ?rijksmonumentnummer ;
                   ceo:heeftGeometrie/geo:asWKT ?rmGeometry .

    # Optioneel blok voor hoofdobject
    optional {
      ?complex ceo:heeftHoofdobject ?rijksmonument ;
               ceo:complexnummer ?complexnummer .
      bind("Hoofdobject" as ?type)
    }

    # Optioneel blok voor complex met rijksmonument
    optional {
      ?complex ceo:heeftRijksmonument ?rijksmonument ;
               ceo:complexnummer ?complexnummer .
      bind("Rijksmonument" as ?type)
    }

    # Optioneel blok voor werelderfgoed met geometrie en buffer
    optional {
      ?werelderfgoed a ceo:Werelderfgoed ;
                     ceo:werelderfgoednummer ?werelderfgoednummer ;
                     ceo:heeftGeometrie/geo:asWKT ?geowh .
      # Het kan zijn dat de bufferwaarde aangepast moet worden.
      filter(geof:sfWithin(?rmGeometry, geof:buffer(?geowh, 55.60, uom:metre)) || geof:sfWithin(?geowh, geof:buffer(?rmGeometry, 55.60, uom:metre)))
    }

    # Optioneel blok voor gezichtsgeometrie
    optional {
      ?gezicht a ceo:Gezicht ;
               ceo:gezichtsnummer ?gezichtnummer ;
               ceo:heeftGeometrie/geo:asWKT ?geogez .
      filter(geof:sfWithin(?rmGeometry, ?geogez))
    }
  }
  bind(concat("Rijksmonument: ", str(?rijksmonumentnummer)) as ?rmGeometryLabel)
  bind(concat("Gezicht: ", str(?gezichtnummer)) as ?geogezLabel)
  bind(concat("Werelderfgoed: ", str(?werelderfgoednummer)) as ?geowhLabel)

}
limit 50