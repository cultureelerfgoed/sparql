#+ name: Query-3-11
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/Closed-Linked-Data/adviseurs/sparql

PREFIX schema: <https://schema.org/>
prefix skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix dcterms: <http://purl.org/dc/terms/> 

SELECT ?gemeenteNaam ?vakgebiedNaam ?adviseurNaam ?monumentnummers
WHERE {
  # Haal de adviseur op
  ?adviseur a schema:Person;
            rdfs:label ?adviseurNaam ;
            schema:worksFor ?gemeente_;
            dcterms:subject ?vakgebied .

  # Haal de naam van de gemeente op
  ?gemeente_ rdfs:label ?gemeenteNaam .

  # Haal de naam van het vakgebied op
  ?vakgebied skos:prefLabel ?vakgebiedNaam .

  # Subquery voor monumenten
  {
    SELECT ?gemeente_ (GROUP_CONCAT(DISTINCT ?rijksmonumentnummer; SEPARATOR=", ") AS ?monumentnummers)
    WHERE {
      SERVICE <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql> {
        GRAPH graph:instanties-rce {
          ?rijksmonument a ceo:Rijksmonument .
          ?rijksmonument ceo:rijksmonumentnummer ?rijksmonumentnummer .
          MINUS {
            ?rijksmonument ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/3e79bb7c-b459-4998-a9ed-78d91d069227> .
          }
          ?rijksmonument ceo:heeftBasisregistratieRelatie ?basisregistratierelatie .
          ?basisregistratierelatie ceo:heeftGemeente ?gemeente_ .
        }
      }
    }
    GROUP BY ?gemeente_
  }
}
ORDER BY ?gemeenteNaam ?vakgebiedNaam ?adviseurNaam
LIMIT 1000
