#+ name: Query-3-2
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/aardbevingen/sparql

prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
prefix ceo:   <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix schema:<https://schema.org/>
prefix skos:  <http://www.w3.org/2004/02/skos/core#>
prefix geo:   <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>

select distinct ?event ?eventStart ?shape ?rm ?geo
where {
  ?event a schema:Event ;
    schema:startDate ?eventStart ;
    schema:category ?cat ;
    schema:location ?place ;
    geo:asWKT ?shape .

  ?cat   skos:prefLabel ?eventLabel .
  ?place schema:name ?plaatsLabel .



  bind(str(?plaatsLabel) as ?plaatsnaam)

  service <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql> {
    graph graph:instanties-rce {
      ?rm a ceo:Rijksmonument ;
        ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:woonplaatsnaam ?plaatsRm ;
        ceo:heeftGeometrie/geo:asWKT ?geo .
    }
  }
  filter(str(?plaatsRm) = ?plaatsnaam)
}
limit 50
