#+ name: 67c0eaa4-b9e9-4ae7-a066-9ee4c1c26c29
#+ service: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt:  <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>

# Vuurtoren (bouwwerk) data uit Wikidata matchen met ceo
CONSTRUCT { 
#  Constructen rijksmonument en label uit wikidata. Matchen wikidata en ceo op instantie van rijksmonumentnummer  (?rm_nr)
?rm ceo:rijksmonumentnummer ?rm_nr   
# Constructen functie vuurtorens + label uit wikidata 
  ; ceo:heeftFunctieNaam 
      <https://data.cultureelerfgoed.nl/term/id/rn/e1caeac0-0ff1-4651-823c-458eb2c9c286> .  
 <https://data.cultureelerfgoed.nl/term/id/rn/e1caeac0-0ff1-4651-823c-458eb2c9c286> skos:prefLabel 'Vuurtoren'@nl . 
  ?rm rdfs:label ?label .
# Constructen bouwjaar, jaar in gebruikname, jaar buitendiensttreding vuurtorens, aantal verdiepingen, hoogte, ARLHS-id, MarineTraffic_id
  ?rm ceox:bouwjaar ?jaarCreatie ; ceox:jaarInGebruikname ?jaarInGebruikname ; ceox:jaarBuitendienststelling ?jaarBuitendienststelling ; ceox:aantalVerdiepingen ?aantalVerdiepingen ; ceox:hoogte ?hoogte ; ceox:ARLHS_id ?ARLHS_id ; ceox:MarineTraffic_id ?MarineTraffic_id
}
# Ophalen data uit wiki
WHERE {
service <https://query.wikidata.org/sparql> 
      {
     # Opvragen instanties van (P31) vuurtoren (Q39715) en land (P17) Nederland (Q55) en monumentstatus (P1435) Rijksmonument (Q916333) en allen met een rijksmonumentnummer (P359) + label
?nummerWiki wdt:P31 wd:Q39715 ; wdt:P17 wd:Q55  ; wdt:P1435 wd:Q916333  ; wdt:P359 ?rm_nr 
   ; rdfs:label ?label
    # Opvragen optioneel datum van oprichting of creatie (P571) en operationele ingebruikname (P729) en
    # operationele buitendienststelling van instantie vuurtoren
 . OPTIONAL {
      ?nummerWiki wdt:P571 ?datumCreatie .  BIND (str(year(?datumCreatie)) AS ?jaarCreatie)  } .
 OPTIONAL {?nummerWiki wdt:P729 ?inGebruikname .  BIND (str(year(?inGebruikname)) AS ?jaarInGebruikname)} .
 OPTIONAL {?nummerWiki wdt:P730 ?buitendienststelling .  BIND (str(year(?buitendienststelling)) AS ?jaarBuitendienststelling) } .
    OPTIONAL {?nummerWiki wdt:P1101 ?aantalVerdiepingen } .
     OPTIONAL {?nummerWiki  wdt:P2048 ?hoogte } .
    OPTIONAL {?nummerWiki  wdt:P2980 ?ARLHS_id } .
    OPTIONAL {?nummerWiki  wdt:P3601 ?MarineTraffic_id } .  
    filter (lang(?label) = 'nl')
  } 
  # Ophalen data uit ceo, iri rijksmonument (?rm) en rijksmonumentnummer (?rm_nr)
  ?rm ceo:rijksmonumentnummer ?rm_nr } 