#+ name: Query-2-met-constatering-1
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Rijksmonumenten-sdo/sparql

PREFIX schema:<https://schema.org/>

SELECT DISTINCT
  ?choi
  ?omschrijving
  ?heeftUitsluiting
  ?heeftRelatieveWaardering
  ?heeftReden
  ?heeftConstatering
  ?primaireCategorie
  ?categorieen
WHERE {
  ?choi schema:description ?omschrijving .
  BIND(LCASE(STR(?omschrijving)) AS ?omslLc)

  VALUES ?zoekterm {
    "niet beschermd" "niet-beschermd" "niet van waarde"
    "geen monumentale waarde" "geen onderdeel"
    "uitgesloten" "uitgezonderd"
    "niet opgenomen" "niet onder bescherming"
    "niet meebeschermd" "niet-meebeschermd"
    "niet in aanmerking" "buiten bescherming"
    "niet nader te beschermen"

    "ondergeschikt" "van ondergeschikt belang"
    "ondergeschikte waarde" "ondergeschikte betekenis"
    "secundair" "geringe waarde" "weinig waarde" "storend"

    "onderdeel van" "in samenhang met"
    "aanmerking" "aanmerking voor"

    "verbouwd" "vernieuwd" "vervangen"
    "gereconstrueerd" "gerestaureerd" "gerenoveerd"
    "modern" "moderne" "recent" "recente"
    "van latere datum" "van recente datum" "in recente tijd"
    "niet oorspronkelijk" "niet origineel"
    "niet meer in oorspronkelijke staat"
    "sterk gewijzigd" "ingrijpend gewijzigd" "aanzienlijk gewijzigd"
  }
  FILTER(CONTAINS(?omslLc, LCASE(?zoekterm)))

  BIND(
    "niet-?\\s*beschermd|niet\\s+van\\s+waarde|geen\\s+onderdeel|uitgezonderd|\\buitgesloten\\b|niet\\s+opgenomen|niet\\s+onder\\s+bescherming|niet-?\\s*meebeschermd|niet\\s+in\\s+aanmerking|buiten\\s+bescherming|niet\\s+nader\\s+te\\s+beschermen|geen\\s+monumentale\\s+waarde"
    AS ?patUitsl
  )
  BIND(
    "\\bondergeschikt(e)?\\b|van\\s+ondergeschikt\\s+belang|ondergeschikte\\s+waarde|ondergeschikte\\s+betekenis|\\bsecundair(e)?\\b|weinig\\s+waarde|van\\s+weinig\\s+waarde|\\bgering(e|ere)?\\b|van\\s+geringe\\s+waarde|\\bstorend\\b"
    AS ?patRel
  )
  BIND(
    "onderdeel\\s+van|in\\s+samenhang\\s+met|aanmerking\\s+voor|\\baanmerking\\b"
    AS ?patReden
  )
  BIND(
    "\\bverbouw(d|d[e]?|ing)?\\b|\\bvernieuwd(e)?\\b|\\bvervangen(e)?\\b|\\bgereconstrueerd(e)?\\b|\\bgerestaureerd(e)?\\b|\\bgerenoveerd(e)?\\b|\\bmodern(e)?\\b|\\brecent(e|ere)?\\b|niet\\s+oorspronkelijk(e)?|niet\\s+origineel(e)?|niet\\s+meer\\s+in\\s+oorspronkelijke?\\s+staat|in\\s+recente\\s+tijd|van\\s+latere\\s+datum|van\\s+recente\\s+datum|\\b(sterk|ingrijpend|aanzienlijk)\\s+gewijzigd\\b"
    AS ?patConst
  )

  BIND(REGEX(?omslLc, ?patUitsl)  AS ?heeftUitsluiting)
  BIND(REGEX(?omslLc, ?patRel)    AS ?heeftRelatieveWaardering)
  BIND(REGEX(?omslLc, ?patReden)  AS ?heeftReden)
  BIND(REGEX(?omslLc, ?patConst)  AS ?heeftConstatering)

  FILTER(?heeftUitsluiting || ?heeftRelatieveWaardering || ?heeftReden || ?heeftConstatering)
  FILTER(!REGEX(?omslLc, "van\\s+belang\\s+als\\s+onderdeel\\s+van"))

  ########################################################
  # 12b: alleen uitsluiting in de selectie
  ########################################################
  FILTER(?heeftUitsluiting)

  BIND(
    IF(?heeftUitsluiting, "uitsluiting of beperking",
      IF(?heeftRelatieveWaardering, "relatieve waardering",
        IF(?heeftReden, "verwijzing naar reden van bescherming", "")
      )
    ) AS ?primaireCategorie
  )

  BIND(CONCAT(
    IF(?heeftUitsluiting,         "uitsluiting of beperking|",              ""),
    IF(?heeftRelatieveWaardering, "relatieve waardering|",                  ""),
    IF(?heeftReden,               "verwijzing naar reden van bescherming|", ""),
    IF(?heeftConstatering,        "constatering|",                          "")
  ) AS ?_cats)
  BIND(REPLACE(?_cats, "\\|$", "") AS ?categorieen)
}
