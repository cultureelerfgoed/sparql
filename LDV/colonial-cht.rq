#+ name: colonial-cht
#+ description: Query om mogelijke koppelingen tussen collectie metadata en de Cultuurhistorische Thesaurus te evalueren. Collectie en thesaurus zijn inwisselbaar zolang de strings 1-op-1 te koppelen zijn.
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT (COUNT(DISTINCT ?obj) AS ?terms) (COUNT(DISTINCT ?uri_cht) AS ?cht_matches) WHERE {

  ?sub dct:medium|dct:subject|schema:artform ?obj .

  BIND(STRLANG(?obj, "nl") AS ?obj_nl)
  
  SERVICE <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Cultuurhistorische-Thesaurus-CHT/sparql> {
    GRAPH <https://data.cultureelerfgoed.nl/term/id/cht/thesaurus> {
      OPTIONAL { ?uri_cht skos:prefLabel|skos:altLabel|skos:hiddenLabel ?obj_nl . }
    }
  }
}
