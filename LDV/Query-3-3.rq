#+ name: Query-3-3
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

prefix bif: <http://www.openlinksw.com/schemas/bif#>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <http://schema.org/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix owms: <http://standaarden.overheid.nl/owms/terms/>
select ?rijksmonumentWkt ?rijksmonumentWktLabel {
  
  #  service <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql> {
    ?rijksmonument
      ceo:heeftBasisregistratieRelatie/ceo:heeftGemeente/owms:CBSCode ?gemeenteCode;
      ceo:heeftGeometrie/geo:asWKT ?rijksmonumentWkt;
      ceo:rijksmonumentnummer ?rijksmonumentnummer.
    optional { ?rijksmonument ceo:heeftNaam/ceo:naam ?rijksmonumentNaam. }
    optional { ?rijksmonument ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam/skos:prefLabel ?functie. }
    optional { ?rijksmonument ceo:heeftStijlEnCultuur/ceo:heeftStijlEnCultuurNaam/skos:prefLabel ?stijlEnCultuur. }
    optional { ?rijksmonument ceo:heeftType/ceo:heeftTypeNaam/skos:prefLabel ?type. }
  
  SERVICE <https://api.labs.kadaster.nl/datasets/cbs/wbk/services/default/sparql>{
  ?gemeente
    rdfs:label ?gemeenteNaam;
    schema:identifier ?_gemeenteCode.
  ?wijk geo:sfWithin ?gemeente.
  ?buurt
    rdfs:label ?buurtNaam;
    geo:hasGeometry/geo:asWKT ?buurtWkt;
    geo:sfWithin ?wijk.
  bind(strafter(str(?_gemeenteCode),'GM') as ?gemeenteCode)
  }

  
  filter(bif:st_intersects(?rijksmonumentWkt, ?buurtWkt))
  bind(strdt(concat(
    '<dl>',
      '<dt>Buurt naam</dt><dd>',str(?buurtNaam),'</dd>',
      if(bound(?functie),concat('<dt>Monument functie</dt><dd>',str(?functie),'</dd>'),''),
      if(bound(?rijksmonumentNaam),concat('<dt>Monument naam</dt><dd>',str(?rijksmonumentNaam),'</dd>'),''),
      '<dt>Monument nummer</dt><dd>',?rijksmonumentnummer,'</dd>',
      if(bound(?stijlEnCultuur),concat('<dt>Stijl & cultuur</dt><dd>',str(?stijlEnCultuur),'</dd>'),''),
      if(bound(?type),concat('<dt>Monument type</dt><dd>',str(?type),'</dd>'),''),
    '</dl>'),rdf:HTML) as ?rijksmonumentWktLabel)
}
limit 100