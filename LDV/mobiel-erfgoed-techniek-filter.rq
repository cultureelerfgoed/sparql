#+ name: mobiel-erfgoed-techniek-filter
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

prefix dct: <http://purl.org/dc/terms/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix crm: <http://www.cidoc-crm.org/cidoc-crm/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select distinct ?beginYear ?endYear ?eventDateBegin ?eventDateEnd ?eventLabel ?eventMedia ?widgetImageLink  ?formattedYearsDifference
?s ?techniek ?yearsDifference (SAMPLE(?typeVoertuig) AS ?filterTypeVoertuig)
where {
  service <https://api.data.netwerkdigitaalerfgoed.nl/datasets/MCN/Mobiele-Collectie-Nederland-crm/sparql> {
    ?s skos:prefLabel ?eventLabel .
    OPTIONAL{?s crm:P62i_is_depicted_by/crm:P65i_is_shown_by ?eventMedia. }
    OPTIONAL{?s (crm:P16_was_used_for/crm:P4_has_time-span/crm:P82a_begin_of_the_begin) ?eventDateBegin.}
    OPTIONAL{?s (crm:P16_was_used_for/crm:P4_has_time-span/crm:P82b_end_of_the_end) ?eventDateEnd.}
    OPTIONAL{?s crm:P16_was_used_for/crm:P32_used_general_technique/crm:P1_is_identified_by ?techniek .}
    OPTIONAL{?s crm:P16_was_used_for/crm:P2_has_type/crm:P1_is_identified_by ?typeVoertuig .}
    bind(xsd:integer(?eventDateBegin) as ?beginYear)
    bind(xsd:integer(?eventDateEnd) as ?endYear)
    bind(?endYear - ?beginYear as ?yearsDifference)

    bind(?eventMedia AS ?widgetImageLink )

  }

  filter(?yearsDifference <1000)
  bind(if(?yearsDifference > 1, concat(?eventDateBegin," - ",?eventDateEnd,"; ", str(?yearsDifference), " jaren operationele dienst"),concat(str(?yearsDifference), " jaar operationele dienst")) as ?formattedYearsDifference)
}
group by ?beginYear ?endYear ?eventDateBegin ?eventDateEnd ?eventLabel ?eventLabel ?eventMedia ?widgetImageLink  ?formattedYearsDifference ?s ?techniek ?yearsDifference
order by ?beginYear
