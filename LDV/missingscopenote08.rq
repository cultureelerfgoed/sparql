#+ name: missingscopenote08
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Cultuurhistorische-Thesaurus-CHT/sparql

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT
  ?prefLabelLaag8
  ?prefLabelLaag9
  ?mistEigenScopeNote
  (COALESCE(?aantalNarrowerScopeNote, 0) AS ?aantalNarrowerZonderScopeNote)
  (IF(COALESCE(?aantalNarrowerScopeNote, 0) > 0, "ja ->", "") AS ?uitklappen)
WHERE {
  {
    SELECT DISTINCT
      ?prefLabelLaag8
      ?prefLabelLaag9
      (IF(BOUND(?object7ScopeNote), "nee"@nl, "ja, mist scopeNote"@nl) AS ?mistEigenScopeNote)
      (COUNT(DISTINCT ?narrowerTermNoScope) AS ?aantalNarrowerScopeNote)
    WHERE {
      SERVICE <https://digitaalerfgoed.poolparty.biz/PoolParty/sparql/term/id/cht> {
        # Triple path to reach the Layer 6 concept:
        <https://data.cultureelerfgoed.nl/term/id/cht/bece25a6-eb64-46e8-85a8-2a7991f02a2c>
          skos:narrower/skos:narrower/skos:narrower/skos:narrower/skos:narrower/skos:narrower/skos:narrower ?object6 .

        ?object6 skos:prefLabel ?prefLabelLaag8 .
        FILTER(LANG(?prefLabelLaag8) = "nl")

        ?object6 skos:narrower ?object7 .

        ?object7 skos:prefLabel ?prefLabelLaag9 .
        FILTER(LANG(?prefLabelLaag9) = "nl")

        # Optional: Get the scopeNote for the Layer 6 term:
        OPTIONAL {
          ?object7 skos:scopeNote ?object7ScopeNote .
          FILTER(LANG(?object7ScopeNote) = "nl")
        }

        # Retrieve all descendant terms of ?object4 (using skos:narrower+) 
        # that do NOT have a Dutch scopeNote:
        OPTIONAL {
          ?object7 skos:narrower+ ?narrowerTerm .
          FILTER NOT EXISTS {
            ?narrowerTerm skos:scopeNote ?narrowerTermScopeNote .
            FILTER(LANG(?narrowerTermScopeNote) = "nl")
          }
          BIND(?narrowerTerm AS ?narrowerTermNoScope)
        }
      }
    }
    GROUP BY ?prefLabelLaag8 ?prefLabelLaag9 ?object7ScopeNote
    ORDER BY DESC(?aantalNarrowerScopeNote)
  }
}
