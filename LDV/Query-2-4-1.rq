#+ name: Query-2-4-1
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Rijksmonumenten-sdo/services/Rijksmonumenten-sdo-jena/sparql

PREFIX text:  <http://jena.apache.org/text#>
PREFIX schema:<https://schema.org/>

SELECT ?primaireCategorie (COUNT(DISTINCT ?choi) AS ?aantal)
WHERE {
  # Full-text filter met ALLE begrippen (zonder "vanwege") + highlight
  (?choi ?sc ?literal) text:query (
    schema:description
    '("niet beschermd" OR "niet van waarde" OR "geen onderdeel" OR uitgezonderd OR "niet opgenomen" OR "niet onder bescherming" OR ondergeschikt OR "van ondergeschikt belang" OR secundair OR "weinig waarde" OR "onderdeel van" OR "in samenhang met" OR gering OR geringe OR geringere OR "van geringe waarde" OR storend OR "niet meebeschermd" OR uitgesloten OR afgebroken OR "niet in aanmerking" OR "aanmerking voor" OR aanmerking)'
    'highlight:s:<strong class="hiLite">|e:</strong>|f:[...]<br>- '
  ) .
  ?choi schema:description ?omschrijving .

  BIND(LCASE(STR(?omschrijving)) AS ?omslLc)

  # Categoriepatronen
  BIND("niet\\s+beschermd|niet\\s+van\\s+waarde|geen\\s+onderdeel|uitgezonderd|niet\\s+opgenomen|niet\\s+onder\\s+bescherming|uitgesloten|niet\\s+meebeschermd|niet\\s+in\\s+aanmerking|\\bafgebroken\\b" AS ?patU)
  BIND("\\bondergeschikt(e)?\\b|van\\s+ondergeschikt\\s+belang|\\bsecundair(e)?\\b|weinig\\s+waarde|\\bgering(e|ere)?\\b|van\\s+geringe\\s+waarde|\\bstorend\\b" AS ?patR)
  BIND("\\bonderdeel\\s+van\\b|in\\s+samenhang\\s+met|aanmerking\\s+voor|\\baanmerking\\b" AS ?patV)

  BIND(REGEX(?omslLc, ?patU) AS ?heeftUitsluiting)
  BIND(REGEX(?omslLc, ?patR) AS ?heeftRelatieveWaardering)
  BIND(REGEX(?omslLc, ?patV) AS ?heeftReden)

  # Primaire categorie (prioriteit: uitsluiting > relatieve waardering > reden)
  BIND(
    IF(?heeftUitsluiting, "uitsluiting of beperking",
      IF(?heeftRelatieveWaardering, "relatieve waardering",
        IF(?heeftReden, "verwijzing naar reden van bescherming", "niet geclassificeerd")
      )
    ) AS ?primaireCategorie
  )
}
GROUP BY ?primaireCategorie
ORDER BY DESC(?aantal)
