#+ name: Query-2-4-1
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Rijksmonumenten-sdo/sparql

PREFIX schema:<https://schema.org/>

SELECT
  ?provincie
  (SUM(?isReden)      AS ?aantalReden)
  (SUM(?isRelatief)   AS ?aantalRelatief)
  (SUM(?isUitsluiting) AS ?aantalUitsluiting)
  (SUM(?isReden + ?isRelatief + ?isUitsluiting) AS ?totaal)
WHERE {

  # 1. Eerst per provincie + primaireCategorie tellen
  {
    SELECT
      ?provincie
      ?primaireCategorie
      (COUNT(DISTINCT ?choi) AS ?aantal)
    WHERE {
      ?choi schema:description   ?omschrijving ;
            schema:addressRegion ?provincie .
      BIND(LCASE(STR(?omschrijving)) AS ?omslLc)

      VALUES ?zoekterm {
        "niet beschermd" "niet-beschermd" "niet van waarde" "geen monumentale waarde"
        "ondergeschikte waarde" "geen onderdeel" "uitgesloten" "uitgezonderd"
        "niet opgenomen" "niet onder bescherming" "ondergeschikt"
        "van ondergeschikt belang" "secundair" "onderdeel van" "in samenhang met"
        "niet meebeschermd" "niet-meebeschermd" "niet in aanmerking"
        "buiten bescherming" "niet nader te beschermen" "niet oorspronkelijk"
        "niet originele" "niet origineel" "in recente tijd" "ondergeschikte betekenis"
        "sterk gewijzigd" "ingrijpend gewijzigd" "aanzienlijk gewijzigd"
      }

      FILTER(CONTAINS(?omslLc, LCASE(?zoekterm)))

      BIND(
        "niet-?\\s*beschermd|niet\\s+van\\s+waarde|geen\\s+onderdeel|uitgezonderd|niet\\s+opgenomen|niet\\s+onder\\s+bescherming|\\buitgesloten\\b|niet-?\\s*meebeschermd|niet\\s+in\\s+aanmerking|\\bafgebroken\\b|buiten\\s+bescherming|niet\\s+nader\\s+te\\s+beschermen"
        AS ?patUitsl
      )

      BIND(
        "\\bondergeschikt(e)?\\b|\\bmodern(e)?\\b|van\\s+ondergeschikt\\s+belang|ondergeschikte\\s+betekenis|\\bsecundair(e)?\\b|van\\s+weinig\\s+waarde|weinig\\s+waarde|\\bgering(e|ere)?\\b|van\\s+geringe\\s+waarde|\\bstorend\\b|geen\\s+monumentale\\s+waarde|niet\\s+oorspronkelijk(e)?|niet\\s+origineel(e)?|in\\s+recente\\s+tijd|\\b(sterk|ingrijpend|aanzienlijk)\\s+gewijzigd\\b"
        AS ?patRel
      )

      BIND(
        "in\\s+samenhang\\s+met\\s+de\\s+bescherming|aanmerking\\s+voor|\\baanmerking\\b"
        AS ?patReden
      )

      BIND(REGEX(?omslLc, ?patUitsl) AS ?heeftUitsluiting)
      BIND(REGEX(?omslLc, ?patRel)   AS ?heeftRelatieveWaardering)
      BIND(REGEX(?omslLc, ?patReden) AS ?heeftReden)

      FILTER(?heeftUitsluiting || ?heeftRelatieveWaardering || ?heeftReden)

      BIND(
        IF(?heeftUitsluiting, "uitsluiting of beperking",
          IF(?heeftRelatieveWaardering, "relatieve waardering",
            "verwijzing naar reden van bescherming"
          )
        ) AS ?primaireCategorie
      )
    }
    GROUP BY ?provincie ?primaireCategorie
  }

  # 2. Per rij bepalen in welke kolom het hoort
  BIND(IF(?primaireCategorie = "verwijzing naar reden van bescherming", ?aantal, 0) AS ?isReden)
  BIND(IF(?primaireCategorie = "relatieve waardering", ?aantal, 0)                   AS ?isRelatief)
  BIND(IF(?primaireCategorie = "uitsluiting of beperking", ?aantal, 0)               AS ?isUitsluiting)
}
GROUP BY ?provincie
ORDER BY ?provincie
