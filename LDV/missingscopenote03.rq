#+ name: missingscopenote03
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Cultuurhistorische-Thesaurus-CHT/sparql

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT
  ?prefLabelLaag3
  ?prefLabelLaag4
  ?mistEigenScopeNote
  (COALESCE(?aantalNarrowerScopeNote, 0) AS ?aantalNarrowerZonderScopeNote)
  (IF(COALESCE(?aantalNarrowerScopeNote, 0) > 0, "ja ->", "") AS ?uitklappen)
WHERE {
  {
    SELECT DISTINCT
      ?prefLabelLaag3
      ?prefLabelLaag4
      (IF(BOUND(?object2ScopeNote), "nee"@nl, "ja, mist scopeNote"@nl) AS ?mistEigenScopeNote)
      (COUNT(DISTINCT ?narrowerTermNoScope) AS ?aantalNarrowerScopeNote)
    WHERE {
      SERVICE <https://digitaalerfgoed.poolparty.biz/PoolParty/sparql/term/id/cht> {
        # Triple path to reach Layer 3 and Layer 4 concepts:
        <https://data.cultureelerfgoed.nl/term/id/cht/bece25a6-eb64-46e8-85a8-2a7991f02a2c>
          skos:narrower/skos:narrower ?object1 .
          
        ?object1 skos:prefLabel ?prefLabelLaag3 .
        FILTER(LANG(?prefLabelLaag3) = "nl")
          
        ?object1 skos:narrower ?object2 .
          
        ?object2 skos:prefLabel ?prefLabelLaag4 .
        FILTER(LANG(?prefLabelLaag4) = "nl")
          
        # Optional scopeNote at Layer 4:
        OPTIONAL {
          ?object2 skos:scopeNote ?object2ScopeNote .
          FILTER(LANG(?object2ScopeNote) = "nl")
        }
          
        # Retrieve all descendant terms of ?object2 (using skos:narrower+) 
        # that do NOT have a Dutch scopeNote:
        OPTIONAL {
          ?object2 skos:narrower+ ?narrowerTerm .
          FILTER NOT EXISTS {
            ?narrowerTerm skos:scopeNote ?narrowerTermScopeNote .
            FILTER(LANG(?narrowerTermScopeNote) = "nl")
          }
          BIND(?narrowerTerm AS ?narrowerTermNoScope)
        }
      }
    }
    GROUP BY ?prefLabelLaag3 ?prefLabelLaag4 ?object2ScopeNote
    ORDER BY DESC(?aantalNarrowerScopeNote)
  }
}
