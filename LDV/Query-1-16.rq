#+ name: Query-1-16
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/bibliotheek/sparql

PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX schema: <https://schema.org/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?titel ?geo (SAMPLE(?img) AS ?image)
  (STRDT(CONCAT(
    '<div style="background-color:rgba(0,0,0,.2);">',
    '   <center><img src="', COALESCE(STR(?image), ''), '" style="max-width: 200px;"></center>',
    '   <ul>',
    '      <li>Rijksmonument: <a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/', STR(?rm), '" target="_blank">', STR(?rm), '</a></li>',
    '      <li>Boek: ', STR(?titel), '</li>',
    '      <li>Zoek op in: <a href="', COALESCE(STR(?catalogus), '#'), '" target="_blank">Catalogus bibliotheek</a></li>',
    '   </ul>',
    '   <br><br>',
    '</div>'
  ), rdf:HTML) AS ?geoLabel)
WHERE {
  # Pattern for the first dataset (only ?titel)
  ?s ceo:rijksmonumentnummer ?rm;
     schema:name ?titel;
     schema:sameAs ?catalogus.

  # Pattern for the second dataset (only ?geo)
  SERVICE <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql> {
    GRAPH graph:instanties-rce {
      ?cho ceo:rijksmonumentnummer ?rm;
           ceo:heeftGeometrie/geo:asWKT ?geo.
    }
  }

  # Pattern for the third dataset (optional, for possible images)
  OPTIONAL {
    SERVICE <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Afbeeldingen/services/Afbeeldingen/sparql> {
      GRAPH <https://linkeddata.cultureelerfgoed.nl/graph/image> {
        ?afb a edm:ProvidedCHO;
             ceo:rijksmonumentnummer ?rm;
             edm:aggregatedCHO ?choi;
             foaf:depiction ?img.
      }
    }
  }
}
GROUP BY ?rm ?titel ?geo ?catalogus
