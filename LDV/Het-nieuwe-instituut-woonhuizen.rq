#+ name: Het-nieuwe-instituut-woonhuizen
#+ Alle woonhuizen en vakantiehuizen in Het nieuwe Instituut collectie met Rietveld als Architect.
#+ In totaal 19 huizen waarbij er geen afbeelding is aan gelinkt.
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/ceo/services/ceo/sparql

PREFIX thesau: <https://collectiedata.hetnieuweinstituut.nl/model/con/thesau/>
PREFIX sdo: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX cidoc: <http://www.cidoc-crm.org/cidoc-crm/>

SELECT DISTINCT ?sub (MIN(?titel) AS ?eventLabel) (MIN(?omschrijving) AS ?eventDescription) (MIN(?jaar) AS ?eventDate)

WHERE {
  SERVICE <https://api.collectiedata.hetnieuweinstituut.nl/datasets/the-other-interface/knowledge-graph/services/knowledge-graph/sparql>{
  # alles van Rietveld waarvan hij 'architect' is.
  ?sub thesau:256 <https://collectiedata.hetnieuweinstituut.nl/id/people/12546> .
  
  # Dat een 'woonhuis' of 'vakantiehuis' is
  VALUES (?typeUri) {
    (<https://collectiedata.hetnieuweinstituut.nl/model/con/thesau/1800>)
    (<https://collectiedata.hetnieuweinstituut.nl/model/con/thesau/2536>)
  }
  
  ?sub sdo:about ?typeUri .
  
  OPTIONAL {
    ?sub skos:prefLabel ?titel .}
    OPTIONAL {
      ?sub sdo:dateCreated ?periode .
      ?periode cidoc:P82a_begin_of_the_begin ?jaar .
    }
    OPTIONAL {
      ?sub sdo:abstract ?omschrijving .
    }
  
}
}
GROUP BY ?sub ?titel ?omschrijving

