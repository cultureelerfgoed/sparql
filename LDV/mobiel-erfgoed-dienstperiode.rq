#+ name: mobiel-erfgoed-dienstperiode
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?s ?eventLabel ?eventMedia ?eventDateBegin ?eventDateEnd ?formattedYearsDifference WHERE {
  SERVICE <https://api.data.netwerkdigitaalerfgoed.nl/datasets/MCN/Mobiele-Collectie-Nederland-crm/sparql> {
    ?s skos:prefLabel ?eventLabel;
       (crm:P62i_is_depicted_by/crm:P65i_is_shown_by) ?eventMedia;
       (crm:P16_was_used_for/crm:P4_has_time-span/crm:P82a_begin_of_the_begin) ?eventDateBegin;
       (crm:P16_was_used_for/crm:P4_has_time-span/crm:P82b_end_of_the_end) ?eventDateEnd.

    BIND(xsd:integer(?eventDateBegin) AS ?beginYear)
    BIND(xsd:integer(?eventDateEnd) AS ?endYear)
    BIND(?endYear - ?beginYear AS ?yearsDifference)
  }
  filter(?yearsDifference <1000)
  BIND(IF(?yearsDifference > 1, CONCAT(STR(?yearsDifference), " jaren operationele dienst"),CONCAT(STR(?yearsDifference), " jaar operationele dienst")) AS ?formattedYearsDifference)
}
