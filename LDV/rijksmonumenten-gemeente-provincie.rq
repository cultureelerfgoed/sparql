#+ name: rijksmonumenten-gemeente-provincie
#+ description: Overzicht van gebouwde rijksmonumenten per provincie of gemeente.
#- Bedoeld om een overzicht in CSV te maken.
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rn: <https://data.cultureelerfgoed.nl/term/id/rn/>
prefix owms: <https://linkeddata.cultureelerfgoed.nl/graph/owms>

select DISTINCT 
  (iri(concat("https://monumentenregister.cultureelerfgoed.nl/monumenten/", ?rijksmonumentnummer)) as ?monumentenregister)
  ?rijksmonumentnummer ?straat ?huisnummer ?adres ?provincie ?gemeente ?postcode ?woonplaatsnaam ?functie ?datumInschrijving

where {
  graph graph:instanties-rce {
    ?rijksmonument 
      ceo:rijksmonumentnummer ?rijksmonumentnummer;
      ceo:datumInschrijvingInMonumentenregister ?datumInschrijving;
      ceo:heeftJuridischeStatus rn:b2d9a59a-fe1e-4552-9a05-3c2acddff864;
      ceo:heeftMonumentAard rn:fc966a68-8863-4970-a83e-110f96006c21;
      ceo:heeftBasisregistratieRelatie ?basisrel.

    ?basisrel ceo:heeftProvincie ?prov;
              ceo:heeftGemeente ?gem.

    OPTIONAL { 
      ?basisrel ceo:heeftBAGRelatie ?bagrelatie.

      OPTIONAL { ?bagrelatie ceo:volledigAdres ?adres. }
      OPTIONAL { ?bagrelatie ceo:postcode ?postcode_. }
      OPTIONAL { ?bagrelatie ceo:woonplaatsnaam ?woonplaatsnaam. }
      OPTIONAL { ?bagrelatie ceo:openbareRuimte ?straat. }
      OPTIONAL { ?bagrelatie ceo:huisnummer ?huisnummer. }
    }

    BIND(replace(str(?postcode_), "([0-9]{4})([A-Z]{2})", "$1 $2") AS ?postcode)

    OPTIONAL {
      ?rijksmonument ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam ?functies.
    }
  }

   graph owms: {
     ?prov skos:prefLabel ?provincie.
     ?gem skos:prefLabel ?gemeente.
   }

  OPTIONAL {
    graph graph:bebouwdeomgeving {
      ?functies skos:prefLabel ?functie_.
      BIND(replace(?functie_, "\\s\\(.*\\)|\\(.*\\)", "") AS ?functie)
    }
  }
}
