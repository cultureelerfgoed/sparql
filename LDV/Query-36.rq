#+ name: Query-36
#+ description: Deze query haalt informatie op over rijksmonumenten en de bijbehorende afbeeldingen van cultureel erfgoedobjecten, en creÃ«ert HTML-widgets met links naar de Beeldbank en het Monumentenregister voor weergave. Het resultaat is beperkt tot 100 rijen.
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/afbeeldingen/services/Afbeeldingen/sparql

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>

SELECT DISTINCT ?choi ?rmn ?geo ?beeldbank (SAMPLE(?image) AS ?sampleImage) ?afb ?naam
  (strdt(concat(
    '<div style="background-color:rgba(0,0,0,.2);">',
    '   <center><img src="{{sampleImage}}" style="max-width: 200px;"></center>',
    '   <ul>',
    '      <li>Rijksmonument: <a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/', STR(?rmn), '" target="_blank">', STR(?rmn), '</a></li>',
    '      <li>{{naam}}</li>',
    '      <li>In: <a href="', STR(?beeldbank), '" target="_blank">Beeldbank</a></li>',
    '   </ul>',
    '   <br><br>',
    '</div>'
  ), rdf:HTML) AS ?widget)

FROM <https://linkeddata.cultureelerfgoed.nl/graph/image>
FROM <https://linkeddata.cultureelerfgoed.nl/graph/instanties-rce>

WHERE {
  SERVICE <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql> {
    ?choi a ceo:Rijksmonument .  
    ?choi ceo:rijksmonumentnummer ?rmn.  
    ?choi ceo:heeftGeometrie/geo:asWKT ?geo  
  }

  ?afb a edm:ProvidedCHO .  
  ?afb edm:aggregatedCHO ?choi.  
  ?afb foaf:depiction ?image.  
  ?afb dc:description ?naam.  
  ?afb edm:isShownAt ?beeldbank.  
#  FILTER (regex(str(?beeldbank), "beeldbank" , "i"))  
}
GROUP BY ?choi ?rmn ?geo ?beeldbank ?naam ?afb
LIMIT 100
