#+ name: Query-26-2
#+ description: n.a.v. Ruben om oud en nieuwe aanlever sets te vergelijken
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

SELECT ?class ?count_old ?count_new (?count_new - ?count_old AS ?verschil_new_old)
WHERE {
  {
    SELECT ?class (COUNT(?s) AS ?count_old)
    WHERE {
      GRAPH <https://linkeddata.cultureelerfgoed.nl/graph/instanties-rce-old> {
        ?s a ?class .
        FILTER(STRSTARTS(STR(?class), "https://linkeddata.cultureelerfgoed.nl/def/ceo#"))
      }
    }
    GROUP BY ?class
  }
  OPTIONAL {
    SELECT ?class (COUNT(?s) AS ?count_new)
    WHERE {
      GRAPH <https://linkeddata.cultureelerfgoed.nl/graph/instanties-rce> {
        ?s a ?class .
        FILTER(STRSTARTS(STR(?class), "https://linkeddata.cultureelerfgoed.nl/def/ceo#"))
      }
    }
    GROUP BY ?class
  }
}
ORDER BY DESC(?verschil_new_old)
