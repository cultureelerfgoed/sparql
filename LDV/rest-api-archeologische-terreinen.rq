#+ name: rest-api-archeologische-terreinen
#+ description: REST API query for the path archeologische-terreinen
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
construct {
  $this ?p1 ?o1.
  ?o1 ?p2 ?o2.
  ?o2 ?p3 ?o3.
  ?o3 ?p4 ?o4.
  ?o4 ?p5 ?o5.
  ?o5 ?p6 ?o6.
  ?o6 ?p7 ?o7.
} where {
  graph graph:instanties-rce {
    {
      #! paginate: true
      select distinct $this {
        $this
          a ceo:ArcheologischTerrein;
          ceo:archis2Monumentnummer ?archis2Monumentnummer.
        bind(?woonplaatsnaam as ?woonplaatsnaam1)
        optional {
          $this ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:woonplaatsnaam ?woonplaatsnaam2.
        }
        filter(!bound(?woonplaatsnaam1) || ?woonplaatsnaam1 = ?woonplaatsnaam2)

        bind(?archeologischeComplexen as ?archeologischeComplexen1)
        optional {
          $this ceo:bevatObject ?archeologischeComplexen2.
          ?archeologischeComplexen2 a ceo:ArcheologischComplex.
        }
        filter(!bound(?archeologischeComplexen1) || ?archeologischeComplexen1 = ?archeologischeComplexen2)
      }
      limit 5

    }
    $this ?p1 ?o1.
    optional {
      filter(?p1 not in (ceo:bevatObject, ceo:isHoofdobjectVanComplex, ceo:isOnderdeelVanComplex))
      ?o1 ?p2 ?o2.
      optional {
        ?o2 ?p3 ?o3.
        optional {
          ?o3 ?p4 ?o4.
          optional {
            ?o4 ?p5 ?o5.
            optional {
              ?o5 ?p6 ?o6.
              optional {
                ?o6 ?p7 ?o7.
              }
            }
          }
        }
      }
    }
  }
}
