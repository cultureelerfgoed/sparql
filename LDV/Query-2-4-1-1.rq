#+ name: Query-2-4-1-1
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Rijksmonumenten-sdo/sparql

PREFIX schema:<https://schema.org/>

SELECT
  (COUNT(DISTINCT ?choi) AS ?totaal)
  (SUM(IF(?heeftUitsluiting, 1, 0))         AS ?aantalUitsluiting)
  (SUM(IF(?heeftRelatieveWaardering, 1, 0)) AS ?aantalRelatieveWaardering)
  (SUM(IF(?heeftReden, 1, 0))               AS ?aantalReden)
WHERE {
  ?choi schema:description ?omschrijving .
  BIND(LCASE(STR(?omschrijving)) AS ?omslLc)

  VALUES ?zoekterm {
    "niet beschermd" "niet-beschermd" "niet van waarde"
    "geen monumentale waarde" "ondergeschikte waarde"
    "geen onderdeel" "uitgesloten" "uitgezonderd"
    "niet opgenomen" "niet onder bescherming"
    "ondergeschikt" "van ondergeschikt belang" "secundair"
    "onderdeel van" "in samenhang met"
    "niet meebeschermd" "niet-meebeschermd"
    "niet in aanmerking" "buiten bescherming"
    "niet nader te beschermen"
    "ondergeschikte betekenis"
    "sterk gewijzigd" "ingrijpend gewijzigd" "aanzienlijk gewijzigd"
  }

  FILTER(CONTAINS(?omslLc, LCASE(?zoekterm)))

  # Uitsluiting of beperking
  BIND(
    "niet-?\\s*beschermd|niet\\s+van\\s+waarde|geen\\s+onderdeel|uitgezonderd|niet\\s+opgenomen|niet\\s+onder\\s+bescherming|\\buitgesloten\\b|niet-?\\s*meebeschermd|niet\\s+in\\s+aanmerking|\\bafgebroken\\b|buiten\\s+bescherming|niet\\s+nader\\s+te\\s+beschermen"
    AS ?patUitsl
  )

  # Relatieve waardering (alle constateringstermen verwijderd!)
  BIND(
    "\\bondergeschikt(e)?\\b|van\\s+ondergeschikt\\s+belang|ondergeschikte\\s+betekenis|\\bsecundair(e)?\\b|van\\s+weinig\\s+waarde|weinig\\s+waarde|\\bgering(e|ere)?\\b|van\\s+geringe\\s+waarde|\\bstorend\\b|geen\\s+monumentale\\s+waarde|\\b(sterk|ingrijpend|aanzienlijk)\\s+gewijzigd\\b"
    AS ?patRel
  )

  # Verwijzing naar reden van bescherming
  BIND(
    "in\\s+samenhang\\s+met\\s+de\\s+bescherming|aanmerking\\s+voor|\\baanmerking\\b"
    AS ?patReden
  )

  BIND(REGEX(?omslLc, ?patUitsl) AS ?heeftUitsluiting)
  BIND(REGEX(?omslLc, ?patRel)   AS ?heeftRelatieveWaardering)
  BIND(REGEX(?omslLc, ?patReden) AS ?heeftReden)

  FILTER(!REGEX(?omslLc, "van\\s+belang\\s+als\\s+onderdeel\\s+van"))

  FILTER(?heeftUitsluiting || ?heeftRelatieveWaardering || ?heeftReden)
}
