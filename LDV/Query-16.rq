#+ name: Query-16
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix ceosp: <https://linkeddata.cultureelerfgoed.nl/def/ceosp/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix schema: <https://schema.org/>

construct {
  ?monument_uri a schema:LandmarksOrHistoricalBuildings ;
                schema:name ?naam ;
                schema:identifier ?rijksmonumentnummer ;
                schema:category ?monumentaard ;
                schema:address ?adres ;
                schema:additionalType ?functie ;
                schema:addressLocality ?woonplaats ;
                schema:description ?omschrijving_label ;
                schema:sameAs ?monument_url .
}
  where {
    graph <https://linkeddata.cultureelerfgoed.nl/graph/instanties-rce> {
      ?monument_uri a ceo:Rijksmonument .
      ?monument_uri ceo:rijksmonumentnummer ?rijksmonumentnummer .
      ?monument_uri ceo:heeftMonumentAard/skos:prefLabel ?monumentaard.
      ?monument_uri ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/b2d9a59a-fe1e-4552-9a05-3c2acddff864> .

      #   FILTER regex(str(?rijksmonumentnummer), "^[0-9]{1,4}$") # één tot vier cijfers

      optional {
        ?monument_uri ceo:heeftNaam/ceo:naam ?naam
      } .
      optional {
        ?monument_uri ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam/skos:prefLabel ?functies
        bind(replace(?functies, "\\s\\(.*\\)|\\(.*\\)", "") as ?functie)

      } .
      optional {
        ?monument_uri ceo:heeftOmschrijving ?omschrijving_ .
        ?omschrijving_ ceo:omschrijving ?omschrijving .

        ?omschrijving_ ceo:formeelStandpunt "1"^^xsd:boolean .
        bind(replace(replace(?omschrijving, "\r", ""), "\n", "") as ?omschrijving_label)

      }
      optional {
        ?monument_uri ceo:isOnderdeelVanComplex/ceo:heeftHoofdobject/ceo:rijksmonumentnummer ?rijksmonumentnummer_hoofdobject
      }

      bind(uri(concat("https://monumentenregister.cultureelerfgoed.nl/monumenten/", ?rijksmonumentnummer)) as ?monument_url)

      optional {
        ?monument_uri ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie ?br .
        ?br ceo:volledigAdres ?adres .
        ?br ceo:woonplaatsnaam ?woonplaats .
      }
    }
  }
  limit 1
