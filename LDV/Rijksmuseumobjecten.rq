#+ name: Rijksmuseumobjecten
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/ceo/sparql

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX wgs84: <http://www.w3.org/2003/01/geo/wgs84_pos#>

SELECT * WHERE {
  SERVICE <https://api.data.netwerkdigitaalerfgoed.nl/datasets/Rijksmuseum/collection/services/collection/sparql>{

    
    SELECT DISTINCT ?linkRijksmuseum ?id ?ontwerperNaam (?voorwerp AS ?eventLabel) ?beschrijving (MIN(?jaar) AS ?eventDate) 
    (?beschrijving AS ?eventDescription)(MAX(?lat) AS ?breedtegraad) (MAX(?long) AS ?lengtegraad)
   
      WHERE {
        ?linkRijksmuseum dc:creator ?ontwerper ;
                         dc:description ?beschrijving;
                         dc:title ?voorwerp ;
                         dc:identifier ?id ;
                         dct:spatial ?spat.

        ?ontwerper skos:prefLabel ?ontwerperNaam .
        
        FILTER(?ontwerperNaam = "Rietveld, Gerrit Thomas"@nl)

        # Alle voorwerpen van Rijksmuseum
        ?spat skos:prefLabel ?locatie ;
               wgs84:lat ?lat ;
               wgs84:long ?long.

        OPTIONAL {
          ?linkRijksmuseum dct:created ?periode .
          BIND(REPLACE(?periode, "[^0-9-]", "") AS ?cleanedYear)
          BIND(IF(STRLEN(?cleanedYear) > 4, STRBEFORE(?cleanedYear, "-"), ?cleanedYear) AS ?jaar)
        }

        FILTER(lang(?voorwerp)='nl' && lang(?beschrijving)='nl' && lang(?periode)='nl')
      
      
      }
      GROUP BY ?voorwerp ?beschrijving ?linkRijksmuseum ?ontwerperNaam ?id
    }
  
  
  }



