#+ name: Query-1-23
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX ceo:   <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX skos:  <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>

SELECT ?gemeente ?aantal
       ((xsd:decimal(?aantal) * 100) / xsd:decimal(?totaal) AS ?percentageVanTotaal)
WHERE {
  { # Aantallen per gemeente (alleen afgevoerd)
    SELECT ?gemeente (COUNT(DISTINCT ?cho) AS ?aantal)
    WHERE {
      GRAPH graph:instanties-rce {
        ?cho a ceo:Rijksmonument ;
             ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/2/3e79bb7c-b459-4998-a9ed-78d91d069227> ;
             ceo:heeftBasisregistratieRelatie [ ceo:heeftGemeente ?gemURI ] .
      }
      GRAPH graph:owms {
        ?gemURI skos:prefLabel ?gemeente .
      }
    }
    GROUP BY ?gemeente
  }
  { # Landelijk totaal afgevoerd
    SELECT (COUNT(DISTINCT ?rm) AS ?totaal)
    WHERE {
      GRAPH graph:instanties-rce {
        ?rm a ceo:Rijksmonument ;
            ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/2/3e79bb7c-b459-4998-a9ed-78d91d069227> .
      }
    }
  }
}
ORDER BY DESC(?aantal) ?gemeente
LIMIT 20