#+ name: rest-api-rijksmonumenten
#+ description: ##### REST API query for the path rijksmonumenten.</p>
#- This SPARQL query is designed to retrieve information related to "Rijkmonumenten" from a Linked Data source that conforms to the Cultureel Erfgoed Ontology (CEO).</p>
#- https://api.linkeddata.cultureelerfgoed.nl/queries/rce/rest-api-rijksmonumenten/run?
#- 
#- Gezocht kan worden op:
#- - gemeentecode, ex.: 1
#- - gemeentenaam, ex. Beekbergen
#- - provinciecode, ex.: ZH
#- - rijksmonumentnummer, ex.: 12340
#- - woonplaatsnaam, ex.: Nuth
#- - cultuurhistorischObjectnummer, ex:: 72843
#- - monumentaard, ex.: gebouwd / archeologisch 
#- - oorspronkelijkeFunctie, ex.: Toegangshek
#- - postcode, ex.: 6161CX
#- - volledigAdres, ex.: Vondelstraat 77
#- - straat, ex.: Vondelstraat
#- - status, ex.: rijksmonument / geen rijksmonument / voorbeschermd
#- </p>
#- Date: dd. 2024-03-05</br>
#- Version: 1.0
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix owms: <http://standaarden.overheid.nl/owms/terms/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>

construct {
  $this ?p1 ?o1 .
  $this ceo:rijksmonumentnummer ?url .
  ?o1 ?p2 ?o2 .
  ?o2 ?p3 ?o3 .
}
where {
  graph graph:instanties-rce {
    {
      # SELECT bindt ?functieUri uit de data
      select distinct $this ?functieUri {
        $this a ceo:Rijksmonument ;
              ceo:cultuurhistorischObjectnummer ?cultuurhistorischObjectnummer ;
              ceo:heeftMonumentAard/skos:prefLabel ?monumentaard ;
              ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam ?functieUri ;
              ceo:rijksmonumentnummer ?rijksmonumentnummer ;
              ceo:heeftBasisregistratieRelatie ?basisRelatie;
              ceo:heeftJuridischeStatus/skos:prefLabel ?status .

        ?basisRelatie ceo:heeftBAGRelatie ?bagRelatie.

        ?bagRelatie ceo:postcode ?postcode;
                    ceo:volledigAdres ?volledigAdres;
                    ceo:openbareRuimte ?straat.

        bind(?woonplaatsnaam as ?woonplaatsnaam1)
        optional { ?bagRelatie ceo:woonplaatsnaam ?woonplaatsnaam2 . }
        filter(!bound(?woonplaatsnaam1) || ?woonplaatsnaam1 = ?woonplaatsnaam2)

        bind(?gemeentecode as ?gemeentecode1)
        optional { ?basisRelatie ceo:heeftBRKRelatie/ceo:gemeentecode ?gemeentecode2 . }
        filter(!bound(?gemeentecode1) || ?gemeentecode1 = ?gemeentecode2)

        bind(?gemeentenaam as ?gemeentenaam1)
        optional { ?basisRelatie ceo:heeftGemeente ?gemeentenaam2 . }
        filter(!bound(?gemeentenaam1) || ?gemeentenaam1 = ?gemeentenaam2)

        bind(?provinciecode as ?provinciecode1)
        optional { ?basisRelatie ceo:heeftBRKRelatie/ceo:provinciecode ?provinciecode2 . }
        filter(!bound(?provinciecode1) || ?provinciecode1 = ?provinciecode2)

       #  FILTER EXISTS {
       #    graph graph:bebouwdeomgeving {
       #      ?functieUri skos:narrower* ?zoekterm
       #    }
       # }
      }
   #   limit 50
    }
    $this ?p1 ?o1 .
    filter(?p1 != ceo:rijksmonumentnummer)
    $this ceo:rijksmonumentnummer ?rijksmonumentnummer .
    bind(concat('https://monumentenregister.cultureelerfgoed.nl/monumenten/', str(?rijksmonumentnummer)) as ?url)
    optional {
      $this ?p1 ?o1 .
      optional {
        filter(?p1 not in (ceo:bevatObject, ceo:isHoofdobjectVanComplex, ceo:isOnderdeelVanComplex))
        ?o1 ?p2 ?o2 .
        optional {
          ?o2 ?p3 ?o3 .
        }
      }
    }
  }
}
