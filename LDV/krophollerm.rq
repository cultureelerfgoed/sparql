#+ name: krophollerm
#+ description: Rijksmonumenten:
#- - Margaret Kropholler
#- - Margaret Staal
#- - Staal, Margaret
#- - Staal-Kropholler, Margaret
#- - Greta Derlinge
#- - M. Kropholler
#- - G. Derlinge
#- - Margaret Staal-Kropholler
#- - M. Staal-Kropholler
#+ service: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
select distinct ?shape ?shapeLabel {
  {
    select
      ?shape
      ?naam
      ?omschrijving
      ?krop
      ?aard
      (group_concat(distinct ?functie;separator=', ') as ?functies)
      (group_concat(concat('<a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/',uri(?nummer),'" target="_blank">',str(?nummer),'</a>'); separator=', ') as ?nummers)
      (group_concat(distinct ?type; separator=', ') as ?types)
    {
      ?monument
        ceo:heeftGeometrie/geo:asWKT ?shape;
        ceo:rijksmonumentnummer ?nummer;
      OPTIONAL {?monument ceo:heeftMonumentAard/skos:prefLabel ?aard. }
      OPTIONAL { ?monument ceo:heeftNaam/ceo:naam ?naam. }
      OPTIONAL {?monument ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam/skos:prefLabel ?functie .}
      OPTIONAL {?monument ceo:heeftType/ceo:heeftTypeNaam/skos:prefLabel ?type.}
      OPTIONAL {?monument ceo:heeftOmschrijving/ceo:omschrijving ?omschrijving.}
     { ?monument ceo:heeftGebeurtenis/ceo:heeftActorEnRol/ceo:heeftActor "Kropholler, M. ; Noord-Brabant". BIND ("Kropholler, M. ; Noord-Brabant" as ?krop)}
      UNION
     { ?monument ceo:heeftGebeurtenis/ceo:heeftActorEnRol/ceo:heeftActor "Staal-Kropholler, M. ; Stad Amsterdam". BIND ("Staal-Kropholler, M. ; Stad Amsterdam" as ?krop)}
      UNION
     { ?monument ceo:heeftGebeurtenis/ceo:heeftActorEnRol/ceo:heeftActor "Staal, M. ; Noord-Holland". BIND ("Staal, M. ; Noord-Holland" as ?krop)}
     UNION
     { FILTER (regex(?omschrijving, "M. Kropholler") || regex(?omschrijving, "Margaret Staal-Kropholler"))} 
   
      #|M. Staal|Margaret Kropholler|Margaret Staal-Kropholler")}

      
    }
    group by ?shape ?naam ?omschrijving ?aard ?krop
  }
  bind(strdt(concat(
    '<h5>',coalesce(?naam,'Geen naam in het monumentenregister'),'</h5>',
    '<dl>',
      '<dt>Functie</dt><dd>',?functies,'</dd>',
      '<dt>Nummer</dt><dd>',?nummers,'</dd>',
     '<dt>Architect</dt><dd>',?krop,'</dd>',
      '<dt>Aard</dt><dd>',?aard,'</dd>',
        if(strlen(?omschrijving)>250, concat('<dt>Omschrijving</dt><dd>',substr(str(?omschrijving),1,150),'...</dd>'),''),
      if(?types!='',concat('<dt>Type</dt><dd>',?types,'</dd>'),''),
    '</dl>'),rdf:HTML) as ?shapeLabel)
} order by ?nummers
 LIMIT 500