#+ name: Query-7-3
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/Archeologisch-Basis-Register/sparql

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rnce: <https://data.cultureelerfgoed.nl/id/rnce#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?geldigheidArtefact (COUNT(DISTINCT ?artefactUri) AS ?aantal)
WHERE {
  { SERVICE <https://digitaalerfgoed.poolparty.biz/PoolParty/sparql/cb410a13-a6e8-4077-a02c-412a93714114> {
    <https://data.cultureelerfgoed.nl/term/id/abr/22cbb070-6542-48f0-8afe-7d98d398cc0b> skos:narrower+ ?artefactUri .
    ?artefactUri skos:prefLabel ?artefact ; 
                 rnce:beginningOfValidity ?beginValidatiedatum .
    FILTER ( ?beginValidatiedatum = "2024-07-01"^^xsd:date && LANG(?artefact) = "nl")
    BIND("nieuw" AS ?geldigheidArtefact) 
  } }
  UNION
  { SERVICE <https://digitaalerfgoed.poolparty.biz/PoolParty/sparql/cb410a13-a6e8-4077-a02c-412a93714114> {
    <https://data.cultureelerfgoed.nl/term/id/abr/22cbb070-6542-48f0-8afe-7d98d398cc0b> skos:narrower+ ?artefactUri .
    ?artefactUri skos:prefLabel ?artefact ; 
                 rnce:beginningOfValidity ?beginValidatiedatum .
    
    FILTER ( ?beginValidatiedatum < "2024-07-01"^^xsd:date && LANG(?artefact) = "nl")
    BIND("bestaand" AS ?geldigheidArtefact) 
  } }
}
GROUP BY ?geldigheidArtefact
