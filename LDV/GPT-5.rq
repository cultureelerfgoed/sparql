#+ name: GPT-5
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

PREFIX ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?rijksmonumentnummer ?lengte ?datumInschrijvingInMonumentenregister ?adres ?gemeente ?omschrijving ?denominatie ?gebeurtenisnaam ?periode ?jaar ?opmerkingen
WHERE { 
  SERVICE <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/gebedshuizen/services/gebedshuizen/sparql> {
    ?gebedshuis ceox:rijksmonumentnummer ?rijksmonumentnummer ;
    ceox:heeftDenominatie/ceox:heeftDenominatieNaam/skos:prefLabel ?denominatie 
    ; ceox:opmerkingen ?opmerkingen ; ceox:heeftGebeurtenis ?gebeurtenisUri .
 ?gebeurtenisUri ceox:heeftGebeurtenisNaam/skos:prefLabel ?gebeurtenisnaam ; ceox:heeftDatering ?datering . optional {?datering ceox:jaar ?jaar } . optional {?datering ceox:periode ?periode }
  }
  ?rm a ceo:Rijksmonument ;
      ceo:datumInschrijvingInMonumentenregister ?datumInschrijvingInMonumentenregister ;
      ceo:rijksmonumentnummer ?rijksmonumentnummer ;
      ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:volledigAdres ?adres ;
      ceo:heeftBasisregistratieRelatie/ceo:heeftBRKRelatie/ceo:gemeentenaam ?gemeente ;
      ceo:heeftOmschrijving/ceo:omschrijving ?omschrijving . 
  MINUS { ?rm ceo:heeftOmschrijving/ceo:formeelStandpunt "0"^^xsd:boolean } .
  BIND(STRLEN(?omschrijving) AS ?lengte)
  FILTER (?lengte >= 2000 && ?lengte <= 4000)
} 
ORDER BY ?rijksmonumentnummer
LIMIT 200
