#+ name: Query-33-1
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/ceo/services/ceo/sparql

# Definitie van prefixen om te gebruiken in de query. 
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?inputProperty ?widget ?createdStr ?modifiedStr ?propertyType ?property ?exampleStr ?example ?inverseOfPropertyStr
  # Groepeer en concateer waarden voor verschillende taalstrings en vormen deze om tot specifieke variabelen.
  (GROUP_CONCAT(DISTINCT ?comment_nlStr; separator=", ") AS ?comments_nl)
  (GROUP_CONCAT(DISTINCT ?comment_enStr; separator=", ") AS ?comments_en)
  (GROUP_CONCAT(DISTINCT ?label_nlStr; separator=", ") AS ?labels_nl)
  (GROUP_CONCAT(DISTINCT ?label_enStr; separator=", ") AS ?labels_en)
  (GROUP_CONCAT(DISTINCT ?domainStr; separator=", ") AS ?domains)
  (GROUP_CONCAT(DISTINCT ?rangeStr; separator=", ") AS ?ranges)
  (GROUP_CONCAT(DISTINCT ?editorialNote_nlStr; separator=", ") AS ?editorialNotes_nl)
  (GROUP_CONCAT(DISTINCT ?editorialNote_enStr; separator=", ") AS ?editorialNotes_en)
  (GROUP_CONCAT(DISTINCT ?description_nlStr; separator=", ") AS ?descriptions_nl)
  (GROUP_CONCAT(DISTINCT ?description_enStr; separator=", ") AS ?descriptions_en)
  # Kijk of de variabele ?registergegeven gebonden is, zo niet geef "Nee" terug.
  (IF(BOUND(?registergegeven), "Ja", "Nee") AS ?registergegevenStr)

WHERE {
  # Selecteer eigenschappen die van een bepaald type zijn.
  ?inputProperty a ?propertyType .
  FILTER(?propertyType IN (owl:DatatypeProperty, owl:ObjectProperty))
  # Filter de eigenschappen op basis van hun URL of exacte string.
  FILTER (contains(lcase(str(?inputProperty)), str(?property)) || str(?inputProperty) = concat("https://linkeddata.cultureelerfgoed.nl/def/ceo#", str(?propertyExact)))

  # Probeer optionele waarden te binden aan variabelen.
  OPTIONAL { ?inputProperty dcterms:created ?created }
  OPTIONAL { ?inputProperty dcterms:modified ?modified }
  OPTIONAL { ?inputProperty rdfs:comment ?comment_nl . FILTER(langmatches(lang(?comment_nl), "nl")) }
  OPTIONAL { ?inputProperty rdfs:comment ?comment_en . FILTER(langmatches(lang(?comment_en), "en")) }
  OPTIONAL { ?inputProperty rdfs:label ?label_nl . FILTER(langmatches(lang(?label_nl), "nl")) }
  OPTIONAL { ?inputProperty rdfs:label ?label_en . FILTER(langmatches(lang(?label_en), "en")) }
  OPTIONAL { ?inputProperty rdfs:domain ?domain }
  OPTIONAL { ?inputProperty rdfs:range ?range }
  OPTIONAL { ?inputProperty skos:example ?exampleData . BIND (IF(isIRI(?exampleData), STR(?exampleData), ?exampleData) AS ?exampleStrData)}
  OPTIONAL { ?inputProperty skos:editorialNote ?editorialNote_nl . FILTER(langmatches(lang(?editorialNote_nl), "nl"))  }
  OPTIONAL { ?inputProperty skos:editorialNote ?editorialNote_en . FILTER(langmatches(lang(?editorialNote_en), "en"))  }
  OPTIONAL { ?inputProperty dcterms:description ?description_nl . FILTER(langmatches(lang(?description_nl), "nl"))  }
  OPTIONAL { ?inputProperty dcterms:description ?description_en . FILTER(langmatches(lang(?description_en), "en"))  }
  OPTIONAL { ?inputProperty ceo:registergegeven ?registergegeven }
  OPTIONAL { ?inputProperty owl:inverseOf ?inverseOfProperty }
  
  # Gebruik de COALESCE-functie om een waarde te binden aan een variabele, zelfs als de waarde mogelijk niet bestaat.
  BIND(COALESCE(str(?created), " -") as ?createdStr)
  BIND(COALESCE(str(?modified), " -") as ?modifiedStr)
  BIND(COALESCE(str(?comment_nl), " -") as ?comment_nlStr)
  BIND(COALESCE(str(?comment_en), " -") as ?comment_enStr)
  BIND(COALESCE(str(?label_nl), " -") as ?label_nlStr)
  BIND(COALESCE(str(?label_en), " -") as ?label_enStr)
  BIND(COALESCE(str(?domain), " -") as ?domainStr)
  BIND(COALESCE(?exampleStrData, " -") AS ?exampleStr)
  BIND(COALESCE(str(?range), " -") as ?rangeStr)
  BIND(COALESCE(str(?editorialNote_nl), " -") as ?editorialNote_nlStr)
  BIND(COALESCE(str(?editorialNote_en), " -") as ?editorialNote_enStr)
  BIND(COALESCE(str(?description_nl), " -") as ?description_nlStr)
  BIND(COALESCE(str(?description_en), " -") as ?description_enStr)
  BIND(COALESCE(str(?inverseOfProperty), " -") as ?inverseOfPropertyStr)
  
  # CreÃ«er een HTML-widget met de verzamelde informatie.
  BIND('''
  <div style="max-height:unset; width:800px;">
    <h3>owl:Property: <a href="{{inputProperty}}">{{labels_nl}} | {{labels_en}}</a></h3>
    <p>Property type: {{propertyType}}</p>
    <p>owl:inverseOf: <a href="{{inverseOfPropertyStr}}">{{inverseOfPropertyStr}}</a></p>
    <p>ceo:registergegeven: {{registergegevenStr}}</p>
    <p>dcterms:created: {{createdStr}}</p>
    <p>dcterms:modified: {{modifiedStr}}</p>
    <p>rdfs:comment: {{comments_nl}}</p>
    <p>rdfs:comment: {{comments_en}}</p>
    <p>rdfs:domain: <a href="{{domains}}">{{domains}}</a></p>
    <p>rdfs:range: <a href="{{ranges}}">{{ranges}}</a></p>
    <p>skos:example: <a href="{{exampleStr}}">{{exampleStr}}</a></p>
    <p>dcterms:description: {{descriptions_nl}}</p>
    <p>dcterms:description: {{descriptions_en}}</p>
    <p>skos:editorialNote: {{editorialNotes_nl}}</p>
    <p>skos:editorialNote: {{editorialNotes_en}}</p>
  </div>
  ''' ^^rdf:HTML AS ?widget)
}
# Groepeer resultaten op basis van bepaalde variabelen.
GROUP BY ?inputProperty ?widget ?createdStr ?modifiedStr ?propertyType ?property ?exampleStr ?example ?registergegeven ?inverseOfPropertyStr
