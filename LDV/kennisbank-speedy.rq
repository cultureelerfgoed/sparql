#+ name: kennisbank-speedy
#+ description: Query om alle concepten uit de CHT met `prefLabel` en `scopeNote` op te halen, plus optioneel `broader`, `narrower`, `altLabel` en `hiddenLabel`.
#- 
#- Wordt gebruikt om de RCE Kennisbank te voorzien van begrippen.
#- 
#- **DO NOT DELETE**
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Cultuurhistorische-Thesaurus-CHT/sparql

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  ?subject rdf:type ?type .
  ?subject skos:prefLabel ?prefLabel .
  ?subject skos:scopeNote ?scopeNote .
  ?subject skos:altLabel ?altLabel .
  ?subject skos:hiddenLabel ?hiddenLabel .
  ?subject skos:broader ?broader .
  ?subject skos:narrower ?narrower .
}
WHERE {
  GRAPH <https://data.cultureelerfgoed.nl/term/id/cht/thesaurus> {
    ?subject rdf:type ?type ;
             skos:prefLabel ?prefLabel ;
             skos:scopeNote ?scopeNote .
    FILTER (?type = skos:Concept && LANG(?prefLabel) = 'nl' && LANG(?scopeNote) = 'nl')
    OPTIONAL { ?subject skos:altLabel ?altLabel . FILTER(LANG(?altLabel) = 'nl') }
    OPTIONAL { ?subject skos:hiddenLabel ?hiddenLabel . FILTER(LANG(?hiddenLabel) = 'nl') }
    OPTIONAL { ?subject skos:broader ?broader }
    OPTIONAL { ?subject skos:narrower ?narrower }
  }
}