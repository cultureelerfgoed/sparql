PREFIX rn: <https://data.cultureelerfgoed.nl/term/id/rn/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT distinct * WHERE {
 

SERVICE <https://query.wikidata.org/sparql>
    {
  ?rijksmonument wdt:P1435 wd:Q916333.
  ?rijksmonument wdt:P31 wd:Q39715.
 ?rijksmonument wdt:P625 ?shape. 
 ?rijksmonument wdt:P359 ?rijksmonumentnummer. 
    
      BIND(URI(?rijksmonument) as ?shapeLabel)
    }
  
  GRAPH graph:instanties-rce {
  ?rijksmonumentrce a ceo:Rijksmonument.
  ?rijksmonumentrce ceo:heeftKennisregistratie/ceo:heeftFunctieNaam ?naam .
    ?naam skos:prefLabel ?monument_functie .
  ?rijksmonumentrce ceo:rijksmonumentnummer ?rijksmonumentnummer .
  
    FILTER(?naam = <https://data.cultureelerfgoed.nl/term/id/rn/80f5dbd8-8633-4767-927f-81493ee7c968>) # selecteer alleen CHT: vuurtorens
  MINUS {?rijksmonumentrce ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/3e79bb7c-b459-4998-a9ed-78d91d069227>}
  }
}
ORDER BY ?rijksmonumentnummer