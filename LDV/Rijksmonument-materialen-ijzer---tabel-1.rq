#+ name: Rijksmonument-materialen-ijzer---tabel-1
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>

select distinct (uri(concat("https://monumentenregister.cultureelerfgoed.nl/monumenten/", ?rijksmonumentnummer)) as ?monumentenregister) ?provincie ?functie ?categorieenfunctie (group_concat(distinct ?material) as ?gevondenMaterialen) ?omschrijving
where {
  graph graph:instanties-rce {
    ?choi a ceo:Rijksmonument.
    ?choi ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/b2d9a59a-fe1e-4552-9a05-3c2acddff864>.
    ?choi ceo:rijksmonumentnummer ?rijksmonumentnummer .
    ?choi ceo:heeftOmschrijving/ceo:omschrijving ?omschrijving_ .
    ?choi ceo:heeftOmschrijving ?standpunt_omschrijving.
    ?standpunt_omschrijving ceo:formeelStandpunt "1"^^<http://www.w3.org/2001/XMLSchema#boolean>.
    bind(replace(?omschrijving_, "\n", " ") as ?omschrijving)
    
    bind(if(regex(?omschrijving, "\\b(ijzer)\\b", "i"), "ijzer", "") as ?matchLabel1)
    bind(if(regex(?omschrijving, "staal", "i"), "staal", "") as ?matchLabel2)
    bind(if(regex(?omschrijving, "stalen", "i"), "stalen", "") as ?matchLabel3)
    bind(if(regex(?omschrijving, "welijzer", "i"), "welijzer", "") as ?matchLabel4)
    bind(if(regex(?omschrijving, "gietijzer", "i"), "gietijzer", "") as ?matchLabel5)
    bind(if(regex(?omschrijving, "smeedijzer", "i"), "smeedijzer", "") as ?matchLabel6)
    bind(if(regex(?omschrijving, "rvs", "i"), "RVS", "") as ?matchLabel7)
    bind(if(regex(?omschrijving, "corten", "i"), "corten", "") as ?matchLabel8)
    bind(if(regex(?omschrijving, "roestvast", "i"), "roestvast", "") as ?matchLabel9)
    bind(if(regex(?omschrijving, "roestvrij", "i"), "roestvrij", "") as ?matchLabel10)
    bind(if(regex(?omschrijving, "ijzeren", "i"), "ijzeren", "") as ?matchLabel11)
    bind(if(regex(?omschrijving, "roest", "i"), "roest", "") as ?matchLabel12)

    bind(concat(if(?matchLabel1 != "", " ", ""), ?matchLabel1,
                if(?matchLabel2 != "", " ", ""), ?matchLabel2,
                if(?matchLabel3 != "", " ", ""), ?matchLabel3,
                if(?matchLabel4 != "", " ", ""), ?matchLabel4,
                if(?matchLabel5 != "", " ", ""), ?matchLabel5,
                if(?matchLabel6 != "", " ", ""), ?matchLabel6,
                if(?matchLabel7 != "", " ", ""), ?matchLabel7,
                if(?matchLabel8 != "", " ", ""), ?matchLabel8,
                if(?matchLabel9 != "", " ", ""), ?matchLabel9,
                if(?matchLabel10 != "", " ", ""), ?matchLabel10,
                if(?matchLabel11 != "", " ", ""), ?matchLabel11,
                if(?matchLabel12 != "", " ", ""), ?matchLabel12) as ?material)
  }
  filter(?material != "")
  filter(!regex(?omschrijving, "Corten, J.P.|J.P. Corten|Cortenbach|Cortensstraat|Huis Corten", "i"))

  optional {
    ?choi ceo:heeftOorspronkelijkeFunctie ?functies_.
    ?functies_ ceo:heeftFunctieNaam ?functies .
    ?functies skos:prefLabel ?functie .
    ?functies_ ceo:formeelStandpunt "1"^^<http://www.w3.org/2001/XMLSchema#boolean>.

    graph graph:bebouwdeomgeving {
      optional {
        ?functies skos:broader ?broader1 .
        ?broader1 skos:prefLabel ?functie1 .
        filter(?functie1 != "bebouwde omgeving")
        optional {
          ?broader1 skos:broader ?broader2 .
          ?broader2 skos:prefLabel ?functie2 .
          filter(?functie2 != "bebouwde omgeving")
          optional {
            ?broader2 skos:broader ?broader3 .
            ?broader3 skos:prefLabel ?functie3 .
            filter(?functie3 != "bebouwde omgeving")
          }
        }
      }
    }
    bind(concat(
      if(bound(?functie1), concat("", ?functie1), ""), 
      if(bound(?functie2), concat(" | ", ?functie2), ""), 
      if(bound(?functie3), concat(" | ", ?functie3), "")
    ) as ?categorieenfunctie)
  }
  ?choi ceo:heeftBasisregistratieRelatie/ceo:heeftProvincie ?prov.
  graph graph:owms {
    ?prov skos:prefLabel ?provincie.}

}
group by ?provincie ?omschrijving ?rijksmonumentnummer ?monumentenregister ?functie ?categorieenfunctie
