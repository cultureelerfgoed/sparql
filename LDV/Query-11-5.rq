#+ name: Query-11-5
#+ description: met jena, zonder afbeeldingen
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Rijksmonumenten-sdo/services/Rijksmonumenten-sdo-jena/sparql

prefix text: <http://jena.apache.org/text#>
prefix schema: <https://schema.org/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix edm: <http://www.europeana.eu/schemas/edm/>
prefix dc: <http://purl.org/dc/elements/1.1/>

select distinct (uri(concat("https://monumentenregister.cultureelerfgoed.nl/monumenten/", ?rijksmonumentnummer)) as ?monumentenregister)
  ?provincie
  ?functies
  ?adres
  ?literal
  ?widget
  ?naam
  ?plaats
  ?monumentaard
where {
  (?choi ?sc ?literal) text:query (
    schema:description '(Jood OR Joods OR Joodse OR Joden OR Israëlitisch OR Hebreeuws OR Jiddisch OR Kosher OR Koosjer OR Sjabbat OR Kille OR Mikwe OR Sjoel OR Hoogduits OR Asjkenazisch OR Portugees OR Synagoge OR Sefardisch OR Isr. OR Rabbijn OR Davidster OR Davidsster)'
    'highlight:s:<strong class="hiLite">|e:</strong>|f:[...]<br>- ').
  (?choi) text:query (schema:addressRegion ?provincie).

  ?choi schema:description ?omschrijving.
  ?choi schema:identifier ?rijksmonumentnummer.

  optional {
    ?choi schema:additionalType ?functie.
    bind(replace(?functie, "\\s\\(.*\\)|\\(.*\\)", "") as ?functies)
  }

  optional {
    ?choi schema:address ?adres.
  }

  ?choi schema:addressLocality ?plaats.
  ?choi schema:category ?monumentaard.

  optional {
    ?choi schema:name ?naam.
  }

  filter(!regex(?omschrijving, "Jodenbreestraat|Jozef Israëls", "i"))

  # Widget aanmaken, met COALESCE() voor lege velden
  bind(str(concat(
    '<div style="margin-top:5mm;">',  
    '   <div style="background-color:rgba(0,0,0,.2); padding-top:5mm;">', 
    '      <ul>',
    '      <li>Rijksmonument: <a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/', coalesce(str(?rijksmonumentnummer), "-"), '" target="_blank">', coalesce(str(?rijksmonumentnummer), "-"), '</a></li>',
    '      <li>Naam: ', coalesce(str(?naam_), "-"), '</li>',
    '      <li>In: <a href="', coalesce(str(?enkele_beeldbank), "#"), '" target="_blank">Beeldbank</a></li>',
    '      <li>Monumentaard: ', coalesce(str(?monumentaard), "-"), '</li>',
    '      <li>Functie: ', coalesce(str(?functies), "-"), '</li>',
    '      <li>Adres: ', coalesce(str(?adres), "-"), '</li>',
    '      <li>Plaats: ', coalesce(str(?plaats), "-"), '</li>',
    '      <li>Provincie: ', coalesce(str(?provincie), "-"), '</li>',
    '      <li>Gevonden woorden in omschrijving:</br>- ', coalesce(str(?literal), "-"), '</li>',
    '   </ul>',
    '   <br><br>',
    '</div>'
  )) as ?widget)
}

limit 100
