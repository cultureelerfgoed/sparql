#+ name: rijksmonumenten-groningen-drenthe-snn-2
#+ description: Groningen en Noord-Drenthe
#- rijksmonumentnummer, adres (straat, huisnummer toevoeging)
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rn: <https://data.cultureelerfgoed.nl/term/id/rn/>
prefix owms: <https://linkeddata.cultureelerfgoed.nl/graph/owms>

select DISTINCT (iri(concat("https://monumentenregister.cultureelerfgoed.nl/monumenten/", ?rijksmonumentnummer)) as ?monumentenregister)
       ?rijksmonumentnummer ?straat ?huisnummer ?adres ?provincie ?gemeente ?postcode ?woonplaatsnaam ?functie

where {
  graph graph:instanties-rce {
    ?rijksmonument ceo:rijksmonumentnummer ?rijksmonumentnummer;
                    ceo:heeftJuridischeStatus rn:b2d9a59a-fe1e-4552-9a05-3c2acddff864;
                    ceo:heeftMonumentAard rn:fc966a68-8863-4970-a83e-110f96006c21;
                    ceo:heeftBasisregistratieRelatie ?basisrel.

    ?basisrel ceo:heeftBAGRelatie ?bagrelatie;
              ceo:heeftProvincie ?prov;
              ceo:heeftGemeente ?gem.

    ?bagrelatie ceo:volledigAdres ?adres;
                ceo:postcode ?postcode_;
                ceo:woonplaatsnaam ?woonplaatsnaam.
    ?bagrelatie ceo:volledigAdres ?adres.
OPTIONAL { ?bagrelatie ceo:openbareRuimte ?straat. }
OPTIONAL { ?bagrelatie ceo:huisnummer ?huisnummer. }


    BIND(replace(?postcode_, "([0-9]{4})([A-Z]{2})", "$1 $2") AS ?postcode)

    OPTIONAL {
      ?rijksmonument ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam ?functies.
    }
  }

  graph owms: {
    ?prov skos:prefLabel ?provincie.
    ?gem skos:prefLabel ?gemeente.
  }

  OPTIONAL {
    graph graph:bebouwdeomgeving {
      ?functies skos:prefLabel ?functie_;
  #              skos:hiddenLabel ?hid.
      # FILTER regex(?hid, "k", "i")
       BIND(replace(?functie_, "\\s\\(.*\\)|\\(.*\\)", "") AS ?functie)
    }
  }
}
