#+ name: missingscopenote06
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Cultuurhistorische-Thesaurus-CHT/sparql

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT
  ?prefLabelLaag6
  ?prefLabelLaag7
  ?mistEigenScopeNote
  (COALESCE(?aantalNarrowerScopeNote, 0) AS ?aantalNarrowerZonderScopeNote)
  (IF(COALESCE(?aantalNarrowerScopeNote, 0) > 0, "ja ->", "") AS ?uitklappen)
WHERE {
  {
    SELECT DISTINCT
      ?prefLabelLaag6
      ?prefLabelLaag7
      (IF(BOUND(?object5ScopeNote), "nee"@nl, "ja, mist scopeNote"@nl) AS ?mistEigenScopeNote)
      (COUNT(DISTINCT ?narrowerTermNoScope) AS ?aantalNarrowerScopeNote)
    WHERE {
      SERVICE <https://digitaalerfgoed.poolparty.biz/PoolParty/sparql/term/id/cht> {
        # Triple path to reach the Layer 6 concept:
        <https://data.cultureelerfgoed.nl/term/id/cht/bece25a6-eb64-46e8-85a8-2a7991f02a2c>
          skos:narrower/skos:narrower/skos:narrower/skos:narrower/skos:narrower ?object4 .

        ?object4 skos:prefLabel ?prefLabelLaag6 .
        FILTER(LANG(?prefLabelLaag6) = "nl")

        ?object4 skos:narrower ?object5 .

        ?object5 skos:prefLabel ?prefLabelLaag7 .
        FILTER(LANG(?prefLabelLaag7) = "nl")

        # Optional: Get the scopeNote for the Layer 6 term:
        OPTIONAL {
          ?object5 skos:scopeNote ?object5ScopeNote .
          FILTER(LANG(?object5ScopeNote) = "nl")
        }

        # Retrieve all descendant terms of ?object4 (using skos:narrower+) 
        # that do NOT have a Dutch scopeNote:
        OPTIONAL {
          ?object5 skos:narrower+ ?narrowerTerm .
          FILTER NOT EXISTS {
            ?narrowerTerm skos:scopeNote ?narrowerTermScopeNote .
            FILTER(LANG(?narrowerTermScopeNote) = "nl")
          }
          BIND(?narrowerTerm AS ?narrowerTermNoScope)
        }
      }
    }
    GROUP BY ?prefLabelLaag6 ?prefLabelLaag7 ?object5ScopeNote
    ORDER BY DESC(?aantalNarrowerScopeNote)
  }
}
