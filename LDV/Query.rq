#+ name: Query
#+ description: eerse poin om ceo naar scheme maar nu met keywords.
#- resultaat 25.000 dat is veel te weinig
#- Dat is alleen alles dat een keywords heeft, maar ik wil ook zonder.
#- easy peasy, no
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/RCE-Knowledge-Graph/RCE-Knowledge-Graph/sparql

PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <https://schema.org/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>

CONSTRUCT {
  ?monument_uri a schema:LandmarksOrHistoricalBuildings ;
                schema:name ?naam ;
                schema:identifier ?rijksmonumentnummer ;
                schema:category ?monumentaard ;
                schema:address ?adres ;
                schema:postalCode ?postcode ;
                schema:additionalType ?functie ;
                schema:addressLocality ?woonplaats ;
                schema:addressRegion ?provincie ;
                geo:asWKT ?geometrie ;
                schema:description ?omschrijving_label ;
                schema:sameAs ?monument_url ;
                schema:keywords ?subject_concept ;
                schema:about ?subject_label .
}
WHERE {
  SERVICE <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql> {

    # Hoofdquery voor monumenten
    SELECT DISTINCT ?monument_uri ?naam ?rijksmonumentnummer ?monumentaard ?adres ?postcode ?functie ?woonplaats ?provincie ?geometrie ?omschrijving_label ?monument_url 
  ?subject_concept ?subject_label 
    WHERE {
      GRAPH graph:instanties-rce {
        ?monument_uri a ceo:Rijksmonument .
        ?monument_uri ceo:rijksmonumentnummer ?rijksmonumentnummer .
        ?monument_uri ceo:heeftMonumentAard/skos:prefLabel ?monumentaard .
        ?monument_uri ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/b2d9a59a-fe1e-4552-9a05-3c2acddff864> .

        OPTIONAL {
          ?monument_uri ceo:heeftNaam/ceo:naam ?naam
        } .
        OPTIONAL {
          ?monument_uri ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam/skos:prefLabel ?functie_
        } .
        OPTIONAL {
          ?monument_uri ceo:heeftOmschrijving ?omschrijving_ .
          ?omschrijving_ ceo:omschrijving ?omschrijving_label .
          ?omschrijving_ ceo:formeelStandpunt "1"^^xsd:boolean .

          # Optionele trefwoorden (skos:Concept)
          OPTIONAL {
            GRAPH <https://linkeddata.cultureelerfgoed.nl/graph/OmschrijvingenOnderwerp> {
              ?omschrijving_ ceox:heeftOmschrijvingOnderwerp ?subject_concept .
              ?subject_concept skos:prefLabel ?subject_label .
            }
          }
        }
        OPTIONAL {
          ?monument_uri ceo:heeftGeometrie/geo:asWKT ?geometrie.
        }
        OPTIONAL {
          ?monument_uri ceo:isOnderdeelVanComplex/ceo:heeftHoofdobject/ceo:rijksmonumentnummer ?rijksmonumentnummer_hoofdobject
        }

        BIND(URI(CONCAT("https://monumentenregister.cultureelerfgoed.nl/monumenten/", ?rijksmonumentnummer)) AS ?monument_url)
        BIND(REPLACE(?functie_, "\\s\\(.*\\)|\\(.*\\)", "") AS ?functie)

        OPTIONAL {
          ?monument_uri ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie ?br .
          ?br ceo:volledigAdres ?adres .
          ?br ceo:woonplaatsnaam ?woonplaats .
          ?br ceo:postcode ?postcode .
          ?monument_uri ceo:heeftBasisregistratieRelatie/ceo:heeftProvincie/skos:prefLabel ?provincie .
        }
      }
    }
  }
}
