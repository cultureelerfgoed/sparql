#+ name: Archiefdagen-filter
#+ description: Voor Archiefdagen 2025 om filter te tonen.
#- Gefilterd op Terp
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix owms: <https://linkeddata.cultureelerfgoed.nl/graph/owms>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select distinct ?archeologischmonument ?geo ?omschrijving ?naam ?provincie ?gemeente
  (strdt(concat(
    '<div style="background-color:rgba(0,0,0,.2); padding-top:5mm;">',
    '   <ul>',
    '      <li>Rijksmonument: <a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/',
    coalesce(str(?rijksmonumentnummer), "-"),
    '" target="_blank">', coalesce(str(?rijksmonumentnummer), "-"), '</a></li>',
    ' <li>Naam: {{naam}}',
    ' <li>Provincie: {{provincie}}',
    ' <li>Gemeente: {{gemeente}}',
    '   </ul>',
    '<ul>',
    '<li><b>Omschrijving: </b>',
    if(strlen(str(?omschrijving)) > 250,
      concat(substr(str(?omschrijving), 1, 250), "... <a href='https://monumentenregister.cultureelerfgoed.nl/monumenten/", str(?rijksmonumentnummer), "' target='_blank'>lees hier verder</a>"),
      str(?omschrijving)),
    '</li>',
    '</ul>',
    '   <br><br>',
    '</div>'
  ), rdf:HTML) as ?geoLabel)
where {
  graph graph:instanties-rce {
    ?archeologischmonument a ceo:Rijksmonument.
    ?archeologischmonument ceo:rijksmonumentnummer ?rijksmonumentnummer.
    ?archeologischmonument ceo:heeftGeometrie/geo:asWKT ?geo.
    ?archeologischmonument ceo:heeftMonumentAard/skos:prefLabel "archeologisch".
    ?archeologischmonument ceo:heeftOmschrijving ?omschrijving_ .
    ?omschrijving_ ceo:omschrijving ?omschrijving.

    # Toegevoegde tekstfilter op 'terp'
    filter(contains(lcase(str(?omschrijving)), "terp"))

    optional {
      ?archeologischmonument ceo:heeftNaam/ceo:naam ?naam.
    }
    ?archeologischmonument ceo:heeftBasisregistratieRelatie ?basisrel.
    ?basisrel ceo:heeftProvincie ?prov;
      ceo:heeftGemeente ?gem.
  }

  graph owms: {
    ?prov skos:prefLabel ?provincie.
    ?gem skos:prefLabel ?gemeente.
  }
      }
    
  

order by ?rijksmonumentnummer
limit 1500
