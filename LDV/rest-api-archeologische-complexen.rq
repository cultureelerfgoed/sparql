#+ name: rest-api-archeologische-complexen
#+ description: REST API query for the path archeologische-complexen.
#- 
#- Due to the time-out issue, levels 6 and 7 of nesting from line 60 to 65 were commented out.
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
construct {
  $this ?p1 ?o1.
  ?o1 ?p2 ?o2.
  ?o2 ?p3 ?o3.
  ?o3 ?p4 ?o4.
  ?o4 ?p5 ?o5.
  ?o5 ?p6 ?o6.
  ?o6 ?p7 ?o7.
} where {
  graph graph:instanties-rce {
    {
              #! paginate: true
      select distinct $this {
        $this
          a ceo:ArcheologischComplex;
          ceo:cultuurhistorischObjectnummer ?cultuurhistorischObjectnummer.
        bind(?archis2Complexnummer as ?archis2Complexnummer1)
        optional {
          $this ceo:archis2Complexnummer ?archis2Complexnummer2.
        }
        filter(!bound(?archis2Complexnummer1) || ?archis2Complexnummer1 = ?archis2Complexnummer2)
        
        bind(?archeologischeOnderzoeksgebieden as ?archeologischeOnderzoeksgebieden1)
        optional {
          $this ceo:ligtInObject ?archeologischeOnderzoeksgebieden2.
           ?archeologischeOnderzoeksgebieden2 a ceo:ArcheologischOnderzoeksgebied.
        }
        filter(!bound(?archeologischeOnderzoeksgebieden1) || ?archeologischeOnderzoeksgebieden1 = ?archeologischeOnderzoeksgebieden2)
        
        bind(?archeologischeTerreinen as ?archeologischeTerreinen1)
        optional {
          $this ceo:ligtInObject ?archeologischeTerreinen2.
          ?archeologischeTerreinen2 a ceo:ArcheologischTerrein.
        }
        filter(!bound(?archeologischeTerreinen1) || ?archeologischeTerreinen1 = ?archeologischeTerreinen2)
        
        bind(?grondsporen as ?grondsporen1)
        optional {
          $this ceo:ligtInObject/ceo:bevatObject ?grondsporen2.
          ?grondsporen2 a ceo:Grondsporen.
        }
        filter(!bound(?grondsporen1) || ?grondsporen1 = ?grondsporen2)
        
        bind(?vondsten as ?vondsten1)
        optional {
          $this ceo:ligtInObject/ceo:bevatObject ?vondsten2.
          ?vondsten2 a ceo:Vondsten.
        }
        filter(!bound(?vondsten1) || ?vondsten1 = ?vondsten2)
        
        bind(?vondstlocaties as ?vondstlocaties1)
        optional {
          $this ceo:ligtInObject ?vondstlocaties2.
          ?vondstlocaties2 a ceo:Vondstlocatie.
        }
        filter(!bound(?vondstlocaties1) || ?vondstlocaties1 = ?vondstlocaties2)
      }
      limit 5

    }
    $this ?p1 ?o1.
    optional {
      filter(?p1 not in (ceo:bevatObject, ceo:isHoofdobjectVanComplex, ceo:isOnderdeelVanComplex))
      ?o1 ?p2 ?o2.
      optional {
        ?o2 ?p3 ?o3.
        optional {
          ?o3 ?p4 ?o4.
          optional {
            ?o4 ?p5 ?o5.
#            optional {
#              ?o5 ?p6 ?o6.
#              optional {
#                ?o6 ?p7 ?o7.
#              }
#            }
          }
        }
      }
    }
  }
}
