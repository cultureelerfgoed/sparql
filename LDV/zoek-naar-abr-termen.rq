#+ name: zoek-naar-abr-termen
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/archeologischbasisregister/services/archeologischbasisregister-jena/sparql

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX text: <http://jena.apache.org/text#>

SELECT DISTINCT ?uri ?prefLabel ?altLabel ?hiddenLabel 
WHERE {
    {
        SELECT DISTINCT ?uri WHERE {
            # Zoekterm correct formatteren met wildcards voor Jena text:query
            BIND(CONCAT("*", ?zoekvraag, "*") AS ?zoekterm)

            # Full-text search over meerdere SKOS-eigenschappen
            ?uri text:query (?predicate ?zoekterm).
            ?uri a skos:Concept ;
                skos:inScheme <https://data.cultureelerfgoed.nl/term/id/abr/b402446a-0a00-4fee-a9cd-1a7f307d651e> ;
                ?predicate ?label .

            VALUES ?predicate { skos:prefLabel skos:altLabel skos:hiddenLabel }
            FILTER (LANG(?label) = "nl")
        }
    }
    
    # Ophalen van alle relevante labels
    ?uri skos:prefLabel ?prefLabel . FILTER(LANG(?prefLabel) = "nl") 
    OPTIONAL { ?uri skos:altLabel ?altLabel . FILTER(LANG(?altLabel) = "nl") }
    OPTIONAL { ?uri skos:hiddenLabel ?hiddenLabel . FILTER(LANG(?hiddenLabel) = "nl") }

  
}
