#+ name: Query-16-1-1-2
#+ description: Query waarbij in de gezocht wordt op functie "Winkels" en alles wat er onder hangt
#- Dataset vs graph koppelen aan wikidata
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT * WHERE {

  SERVICE  <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Bebouwde-omgeving-referentienetwerk/sparql>
 {
  {<https://data.cultureelerfgoed.nl/term/id/rn/a0bc1f8c-f225-4134-b161-730628805d70> skos:broader ?narrow.}
  UNION
  {
 <https://data.cultureelerfgoed.nl/term/id/rn/05606ed7-8375-401d-965d-9300010fc6c1> skos:narrower+ ?narrow.}
    ?narrow skos:prefLabel ?label.
  }
 
   ?rijksmonument a ceo:Rijksmonument .
  ?rijksmonument ceo:rijksmonumentnummer ?rijksmonumentnummer.
  OPTIONAL {?rijksmonument ceo:heeftGeometrie/geo:asWKT ?shape .}
  ?rijksmonument ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam ?narrow.
  MINUS {?rijksmonument ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/3e79bb7c-b459-4998-a9ed-78d91d069227>}
 
  SERVICE <https://query.wikidata.org/sparql>
{
  ?wikirm wdt:P1435 wd:Q916333.
    OPTIONAL {?wikirm wdt:P18 ?afbeelding.}
  ?wikirm wdt:P359 ?rijksmonumentnummer.
  }

    BIND(strdt(concat('<div style="position: relative;">','<img src="',str(?afbeelding),'" style="max-width: 100px;">','</a>',?uriSubs, " - ", "Rm: ",'<a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/',uri(?rijksmonumentnummer),'" target="_blank">',str(?rijksmonumentnummer),'</a>'),rdf:HTML) AS ?shapeLabel)
}

LIMIT 500
