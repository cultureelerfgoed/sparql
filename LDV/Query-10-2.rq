#+ name: Query-10-2
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/patrick-mout/Archeologisch-Basis-Register/sparql

PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?uri ?artefact ?hierarchie ?widget 
WHERE {
  {
    SELECT ?uri ?artefact (GROUP_CONCAT(?label; SEPARATOR="<br/>") AS ?hierarchie)
    WHERE {
      <https://data.cultureelerfgoed.nl/term/id/abr/22cbb070-6542-48f0-8afe-7d98d398cc0b> skos:narrower* ?uri .
      ?uri skos:prefLabel ?artefact .

      OPTIONAL {
        ?uri skos:broader+ ?broaderConcept .
        ?broaderConcept skos:prefLabel ?label .
        FILTER (LANG(?label) = "nl")
      }
      FILTER (LANG(?artefact) = "nl")
    }
    GROUP BY ?uri ?artefact
  }
  
  BIND('''
  <div style="border: 1px solid black; padding: 10px; margin: 10px;">
    <h2>{{artefact}}</h2>
    <p style="color: black; font-style: italic;">Broader Labels:</p>
    <p style="color: green;">{{hierarchie}}</p>
  </div>
  '''^^rdf:HTML AS ?widget)
}
GROUP BY ?uri ?artefact ?hierarchie ?widget
ORDER BY ?uri
LIMIT 100
