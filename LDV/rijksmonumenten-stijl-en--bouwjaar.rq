#+ name: rijksmonumenten-stijl-en--bouwjaar
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix sor: <https://data.kkg.kadaster.nl/sor/model/def/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
select distinct ?wkt ?wktColor ?wktLabel {
  ?rijksmonument
    a ceo:Rijksmonument;
    ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:verblijfsobjectIdentificatie ?vboId;
    ceo:heeftGeometrie/geo:asWKT ?wkt;
    ceo:heeftLocatieAanduiding/ceo:locatienaam ?locatienaam.
  optional { ?rijksmonument ceo:heeftStijlEnCultuur/ceo:heeftStijlEnCultuurNaam/skos:prefLabel ?stijl. }
  bind(iri(concat('https://data.kkg.kadaster.nl/id/verblijfsobject/',?vboId)) as ?vbo)
  service <https://api.labs.kadaster.nl/datasets/dst/kkg/sparql> {
    ?vbo sor:maaktDeelUitVan/sor:oorspronkelijkBouwjaar ?bouwjaar.
  }
  bind(strdt(concat('<dl><dt>Stijl</dt><dd>',coalesce(str(?stijl),''),'</dd><dt>Bouwjaar</dt><dd>',coalesce(str(?bouwjaar),''),'</dd></dl>'),rdf:HTML) as ?wktLabel)
  bind(if(bound(?stijl),'green','grey') as ?wktColor)
}
