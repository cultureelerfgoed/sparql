#+ name: Query-5-4-1
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix dc: <http://purl.org/dc/elements/1.1/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix sor: <https://data.kkg.kadaster.nl/sor/model/def/>
prefix kad: <https://data.kkg.kadaster.nl/kad/model/def/>
prefix owms: <http://standaarden.overheid.nl/owms/terms/>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix rn: <https://data.cultureelerfgoed.nl/term/id/rn/>
prefix skos: <http://www.w3.org/2004/02/skos/core#>

select distinct ?cho ?straatnaam ?huisnummer ?postcode ?gemeente ?kadastraleGemeentecode ?kadastraleGemeenteUri ?gemeentenaam ?perceel ?sectie where {
  graph graph:instanties-rce {
    ?cho a ceo:Rijksmonument .
    ?cho ceo:heeftJuridischeStatus rn:b2d9a59a-fe1e-4552-9a05-3c2acddff864.
    ?cho ceo:rijksmonumentnummer ?rm.
    ?cho ceo:heeftBasisregistratieRelatie/ceo:heeftBRKRelatie/ceo:perceelnummer ?perceel.
    ?cho ceo:heeftBasisregistratieRelatie/ceo:heeftBRKRelatie/ceo:sectie ?sectie.
    optional {
      ?cho ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:postcode ?postcode.
    }
    optional {
      ?cho ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:openbareRuimte ?straatnaam.
    }
    optional {
      ?cho ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:huisnummer ?huisnummer.
    }
    ?cho ceo:heeftBasisregistratieRelatie/ceo:heeftBRKRelatie/ceo:gemeentenaam ?gemeentenaam.
    ?cho ceo:heeftBasisregistratieRelatie/ceo:heeftBRKRelatie/ceo:gemeentecode ?kadastraleGemeentecode.
    bind(concat("https://brk.basisregistraties.overheid.nl/brk2/id/kadastraleGemeente/", ?kadastraleGemeentecode) as ?kadastraleGemeenteUri)
    optional{
      ?cho ceo:heeftBasisregistratieRelatie/ceo:heeftGemeente ?gemeente_ .
    }
    graph graph:owms {
      ?gemeente_ skos:prefLabel ?gemeente.
    }
  }
}
limit 20
