#+ name: geoGRietveld-2-2
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX cidoc: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rel: <http://purl.org/vocab/relationship/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bif: <http://www.openlinksw.com/schemas/bif#>
PREFIX schema: <https://schema.org/>
PREFIX rico: <https://www.ica.org/standards/RiC/ontology#>
PREFIX thesau: <https://collectiedata.hetnieuweinstituut.nl/model/con/thesau/>


SELECT DISTINCT ?eventLabel ?eventDate ?rijksmonumentnummer 
  ?maxgebouwRM ?WD ?maxgebouwHNI ?eventMedia ?eventDescription ?eventMediaCredit


{

  
  #*****************************************************************************************************
  #--------------------------------------Dataset 1 Wikidata-----------------------------------------
  #-------------------------------------------------------------------------------------------------
  {
    SELECT DISTINCT ?item ?gebouwLabel (xsd:string(?eventStartYear) AS ?eventStartYearWD)
    (MAX(?afbeelding) AS ?mediaWD)

    WHERE {
      SERVICE <https://query.wikidata.org/sparql> {
        {
          SELECT DISTINCT ?item  WHERE {
            ?item p:P84 ?statement0.
            ?statement0 (ps:P84/(wdt:P279*)) wd:Q160435. # wd:Q160435 = Gerrit Rietveld
            OPTIONAL {
              ?item wdt:P359 ?rijksmonumentnummer .
            }
            FILTER(!BOUND(?rijksmonumentnummer))
          }
        }
        # ------------------------------------------gebouwLabel---------------------------------------
        # Naam van het gebouw.
        OPTIONAL {
          ?item (wdt:P1705|rdfs:label) ?gebouwLabel .
          FILTER(LANG(?gebouwLabel) = "nl")
        }
        #----------------------------------------------afbeelding-----------------------------------------
        # filter alleen afbeelding format dat werkt met de tijdlijn visualisatie.
        OPTIONAL {
          ?item wdt:P18 ?afbeelding .
          FILTER(REGEX(str(?afbeelding), "(?i)\\.(jpg|gif|jpeg|png)", "i")) # Let op; geen .tif
        }
        #-----------------------------------------eventStartDate------------------------------------
        OPTIONAL{
          ?item (wdt:P571|wdt:P276/(pqv:P580*)|wdt:P1619|wdt:P1319|ps:P580) ?eventStartDate .
          FILTER(YEAR(?eventStartDate) > 1900) # een extra check op foute invoering in wikidata.
        }
        BIND(YEAR(?eventStartDate) AS ?eventStartYear)
      }
    }
  }
  #***************************************************************************************************
  UNION
  #***************************************************************************************************
  {
    SELECT  ?rijksmonumentnummer
      (MAX(COALESCE(IF(Bound(?gebouwRM), ?eventLabelRM,
                    IF(Bound(?gebouwHNI), ?eventLabelHNI,
                    IF(Bound(?gebouwWD), ?eventLabelWDrn, ""))))) AS ?eventLabelLaag2)
      (MAX(?gebouwWD) AS ?maxgebouwWD)
      (MAX(?gebouwRM) AS ?maxgebouwRM)
      (MAX(?gebouwHNI) AS ?maxgebouwHNI)
      (MAX(COALESCE(?mediaWDrn, ?mediaHNI)) AS ?eventMediaLaag2)
      (MAX(COALESCE(?eventStartWDrn, ?eventStart)) AS ?eventDateLaag2)
      (MAX(COALESCE(?beschrijving2, ?beschrijving3, ?beschrijving4)) AS ?eventMediaCredit)

    {
      #-------------------------------Dataset 2 Rijksmonumenten-----------------------------------
      #--------------------------------------------------------------------------------------------
      {
        SELECT DISTINCT ?gebouwRM ?bron ?rijksmonumentnummer (MAX(?gebouwLabel) AS ?eventLabelRM) ?beschrijving2
        WHERE {
          GRAPH graph:instanties-rce {
            # --------------------------------------beschrijving---------------------------------
            ?gebouwRM (ceo:heeftOmschrijving/ceo:omschrijving|
                       ceo:heeftKennisregistratie/ceo:heeftActorEnRol/ceo:heeftActor) ?eventDescription .
            FILTER REGEX(?eventDescription, "gerrit*rietveld | Th.*Rietveld | G.*Rietveld | Th.*Rietveld | Gerrit Thomas Rietveld | *G.Th. Rietveld*|Rietveld, G.Th.*", "i")
            BIND(SUBSTR(?eventDescription, 1, 500) AS ?beschrijving)
            BIND(CONCAT(?beschrijving,"...") AS ?beschrijving2)

            # --------------------------------------Rijksmonumentnummer---------------------------------
            ?gebouwRM ceo:rijksmonumentnummer ?rijksmonumentnummer.
            VALUES ( ?bron) {
                    ('Rijksdienst voor Cultureelerfgoed')
                    }
            OPTIONAL{ ?gebouwRM ceo:heeftNaam/ceo:naam| ceo:heeftOorspronkelijkeFunctie/ceo:kennisregistratieToelichting | ceo:isOnderdeelVanComplex/ceo:heeftNaam/ceo:naam|ceo:cultuurhistorischObjectnummer ?gebouwLabel .}
          }
        }
      }
      #--------------------------------------------------------------------------------------------
      #--------------------------------------------------------------------------------------------
      UNION
      #--------------------------------Dataset 3 Wikidata-----------------------------------------
      #--------------------------------------------------------------------------------------------
      {
        SELECT  ?gebouwWD ?bron ?rijksmonumentnummer
          (MAX(?gebouwLabel) AS ?eventLabelWDrn)
          (SAMPLE(?afbeelding) AS ?mediaWDrn)
          (SAMPLE(xsd:string(?eventStartYearWDrn)) AS ?eventStartWDrn)
        
        WHERE {
          SERVICE <https://query.wikidata.org/sparql> {
            {
              SELECT DISTINCT ?gebouwWD WHERE {
                ?gebouwWD p:P84 ?statement0.
                ?statement0 (ps:P84/(wdt:P279*)) wd:Q160435. # wd:Q160435 = Gerrit Rietveld
              }
            }
            # ------------------------------------------bronLabel---------------------------------------
            #Label toevoegen zodat er duidelijk is waar data vandaan komt.
            VALUES (?bron) {('Wikidata')}
            # ------------------------------------------gebouwLabel---------------------------------------
            # Naam van het gebouw.
            OPTIONAL {
              ?gebouwWD (wdt:P1705|rdfs:label) ?gebouwLabel .
              FILTER(LANG(?gebouwLabel) = "nl")
            }
            # --------------------------------------Rijksmonumentnummer---------------------------------
            ?gebouwWD (wdt:P359) ?rijksmonumentnummer . # rijksmonumentnummer toevoegen indien aanwezig.
            #----------------------------------------------afbeelding-----------------------------------------
            # filter alleen afbeelding format dat werkt met de tijdlijn visualisatie.
            OPTIONAL {
              ?gebouwWD wdt:P18 ?afbeelding .
              FILTER(REGEX(str(?afbeelding), "(?i)\\.(jpg|gif|jpeg|png)", "i")) # Let op; geen .tif
            }
            #-----------------------------------------eventStartDate------------------------------------
            OPTIONAL{
              ?gebouwWD (wdt:P571|wdt:P276/(pqv:P580*)|wdt:P1619|wdt:P1319|ps:P580) ?eventStartDate .
              FILTER(YEAR(?eventStartDate) > 1900) # een extra check op foute invoering in wikidata.
            }
            BIND(YEAR(?eventStartDate) AS ?eventStartYearWDrn)
          }
        }
      }
      #--------------------------------------------------------------------------------------------
      #--------------------------------------------------------------------------------------------
      UNION
      #--------------------------------------------------------------------------------------------
      #------------------------Dataset 4 Het Nieuwe Instituut-------------------------------------
      {
        SELECT DISTINCT ?gebouwHNI ?bron (MAX(?afbeelding) AS ?mediaHNI)
          (MAX(?gebouwLabel) AS ?eventLabelHNI)
          (MAX(?collectie) AS ?colectieNaam)
          (MAX(?objectType) AS ?eventMediaAlt)
          (MAX(?id) AS ?collectieID)
          (MAX(?startjaar2) AS ?eventStart)
          (MAX(?beschrijvingHNI2) AS ?beschrijving4)
          (MAX(?type) AS ?eventType)
        WHERE{
          SERVICE <https://api.collectiedata.hetnieuweinstituut.nl/datasets/the-other-interface/knowledge-graph/services/knowledge-graph/sparql>{
            VALUES ?property {
            dct:contributor thesau:256 thesau:26455 thesau:5234
            thesau:2136 thesau:2109 thesau:1803 thesau:176 schema:creator
            rico:isCreatorOf rico:isAgentAssociatedWithAgent rel:cre
          }
            ?gebouwHNI ?property <https://collectiedata.hetnieuweinstituut.nl/id/people/12546> .
            #--------------------------------------Beschrijving--------------------------------------
            OPTIONAL{
              ?gebouwHNI schema:description|schema:abstract ?beschrijvingHNI .
            }
            BIND(SUBSTR(?beschrijvingHNI, 1, 500) AS ?beschrijvingHNI1)
            BIND(CONCAT(?beschrijvingHNI1,"...") AS ?beschrijvingHNI2)
            # OPTIONAL{?gebouwHNI dct:contributor/schema:name|thesau:5234/schema:name ?contributor .}
            # OPTIONAL{?gebouwHNI schema:artform/rdfs:label|schema:type/schema:Article|rdf:type ?objectType .}
            # OPTIONAL{?gebouwHNI schema:archivedAt|rico:scopeAndContent|schema:isPartOf/schema:name ?collectie .}
            #------------------------------------------jaar-----------------------------------------
            OPTIONAL{
              ?gebouwHNI schema:dateCreated/cidoc:P82a_begin_of_the_begin|
                         schema:datePublished|
                         rico:date/rico:hasBeginningDate ?startjaar .
              BIND(SUBSTR(?startjaar, 1, 4) AS ?startjaar2)
            }
            #------------------------------------afbeelding---------------------------------------
            OPTIONAL{
              ?gebouwHNI (schema:associatedMedia/schema:image) ?afbeelding1 .
            }
            OPTIONAL {
              ?gebouwHNI (schema:url|schema:associatedMedia/schema:image) ?afbeelding2 .
            }
            BIND(COALESCE(?afbeelding1, ?afbeelding2) AS ?afbeelding)
            #------------------------------------gebouwLabel---------------------------------------
            OPTIONAL{
              ?gebouwHNI skos:prefLabel|rico:title|schema:name|schema:artform/rdfs:label ?gebouwLabel .
            }
            BIND(IF(REGEX(?gebouwLabel, "(huis|woning|woningen|flat|gebouw|huizen|wijk|winkel|toren|Bungalows|villa|Stedenbouwkundig|museum)", "i"), "gebouw",
                 IF(REGEX(?gebouwLabel, "(stoel|zit|lessenaar|fauteuil|bank|tafel|bed)", "i"), "meubel",
                 IF(REGEX(?gebouwLabel, "(interieur|zaal|inrichting)", "i"), "interieur","overig"))) AS ?type)
            FILTER(?type = "gebouw")
            FILTER(!REGEX(?gebouwLabel, "Zitmeubel", "i"))
            VALUES (?bron) {
                    ('Het Nieuwe Instituut')
                    }
          }
        }
        GROUP BY ?bron ?gebouwHNI ?eventType ?afbeelding3

        #--------------------------------------------------------------------------------------------
        #--------------------------------------------------------------------------------------------
      }
    }
    GROUP BY ?rijksmonumentnummer ?gebouwHNI
  }
  #**************************************************************************************************
BIND(COALESCE(?maxgebouwWD, ?item) AS ?WD)
BIND(COALESCE(?eventLabelLaag2, ?gebouwLabel) AS ?eventLabel)
BIND(COALESCE(STR(?eventDateLaag2), STR(?eventStartYearWD)) AS ?eventDate)
BIND(COALESCE(?eventMediaLaag2, ?mediaWD) AS ?eventMedia)

  #------------------------------------eventDescription----------------------------------------------
  
  BIND(strdt(concat('''

        Gebouw of schaalmodel gegevens.


        <ul>
        <li>Bron: <a href="''', ?maxgebouwRM, '" target="_blank">', ?maxgebouwRM, '''</a></li>
        <li>Wikidata: <a href="''', ?WD, '" target="_blank">', ?WD, '''</a></li>
        <li>Het Nieuwe Instituut: <a href="''', ?maxgebouwHNI, '" target="_blank">', ?maxgebouwHNI, '''</a></li>
        <li>Monumentnummer: <a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/''',
                    uri(?rijksmonumentnummer), '" target="_blank">',
                    str(?rijksmonumentnummer), '</a>','</li></ul>'), rdf:HTML) as ?eventDescription)
}
GROUP BY ?rijksmonumentnummer 
 