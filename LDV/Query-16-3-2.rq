#+ name: Query-16-3-2
#+ description: Monumenten zoeken binnen kader
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


SELECT?zoekveld ("ivory" AS ?zoekveldColor) ?shapeLabel ?rm ?shape  WHERE {
  ?rm a ceo:Rijksmonument .
  ?rm ceo:rijksmonumentnummer ?rijksmonumentnummer.
  ?rm ceo:heeftGeometrie/geo:asWKT ?shape .
    bind('Polygon((5.8337 52.5712,5.8337 52.4596,6.1631 52.4596,6.1631 52.5712,5.8337 52.5712))'^^geo:wktLiteral as ?zoekveld)
   filter(geof:sfWithin(?shape, ?zoekveld))
   BIND(strdt(concat("Rm: ",'<a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/',uri(?rijksmonumentnummer),'" target="_blank">',str(?rijksmonumentnummer),'</a>'),rdf:HTML) AS ?shapeLabel)
    
} ORDER BY ?zoekveld #LIMIT 100