PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>

SELECT distinct *

WHERE {
  ?rm ceo:rijksmonumentnummer ?rijksmonumentnummer .
  ?rm ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/b2d9a59a-fe1e-4552-9a05-3c2acddff864> .
  OPTIONAL { ?rm ceo:heeftNaam/ceo:naam ?naam }
  ?rm ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:verblijfsobjectIdentificatie ?BAGid .
  OPTIONAL { ?rm ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:volledigAdres ?adres }
  OPTIONAL { ?rm ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:postcode ?postcode_x .
    BIND(replace(str(?postcode_x), "([0-9])([A-Z])", "$1 $2") AS ?postcode)
  }
  OPTIONAL { ?rm ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:woonplaatsnaam ?woonplaats }
  ?rm ceo:heeftBasisregistratieRelatie/ceo:heeftGemeente/skos:prefLabel ?gemeente .
   FILTER regex(?gemeente, "Terneuzen", "i") 
  ?rm ceo:heeftGeometrie/geo:asWKT ?geo.
  ?rm ceo:heeftOmschrijving/ceo:omschrijving ?redengevende_omschrijving.
  ?rm ceo:heeftOmschrijving/ceo:formeelStandpunt "true"^^<http://www.w3.org/2001/XMLSchema#boolean> .
   OPTIONAL { ?rm ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam/skos:prefLabel ?functie }
  
}
ORDER BY ?rijksmonumentnummer
