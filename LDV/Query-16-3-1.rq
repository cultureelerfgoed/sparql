#+ name: Query-16-3-1
#+ description: Query waarbij in het complex gezocht wordt op de boolean "buitenplaats" = true
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix xsd:  <http://www.w3.org/2001/XMLSchema#>
prefix ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix geo:  <http://www.opengis.net/ont/geosparql#>
prefix ceo:  <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct *
where {
  service <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Buitenplaatsen/sparql> {
    ?complex a ceo:Complex ;
             ceox:RCEBuitenplaats "1"^^xsd:boolean .
  }

  ?complex ceo:complexnummer   ?complexnummer ;
           ceo:heeftHoofdobject ?hoofdobject ;
           ceo:heeftRijksmonument ?rijksmonument .

  # optional {
  #   ?complex ceo:heeftKennisregistratie/ceo:naam ?naam
  # }

  ?hoofdobject   ceo:rijksmonumentnummer ?hoofdobjectnummer .
  ?rijksmonument ceo:rijksmonumentnummer ?rijksmonumentnummer ;
                 ceo:heeftGeometrie/geo:asWKT ?shape ;
                 ceo:heeftJuridischeStatus
                   <https://data.cultureelerfgoed.nl/term/id/rn/2/b2d9a59a-fe1e-4552-9a05-3c2acddff864> .

  BIND(
    strdt(
      concat(
        "Naam: ", COALESCE(?naam, ""),
        " Complexnummer: ",
        '<a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/', uri(?complexnummer), '" target="_blank">', str(?complexnummer), '</a>',
        " Hoofdobjectnr: ",
        '<a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/', uri(?hoofdobjectnummer), '" target="_blank">', str(?hoofdobjectnummer), '</a>',
        " Rm: ",
        '<a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/', uri(?rijksmonumentnummer), '" target="_blank">', str(?rijksmonumentnummer), '</a>'
      ),
      rdf:HTML
    ) AS ?shapeLabel
  ) .

  BIND("black" AS ?shapeColor) .

  {
    # lucht
    {
      BIND(
        <https://service.pdok.nl/hwh/luchtfotorgb/wms/v1_0?request=GetCapabilities&service=wms>
        AS ?mapEndpoint
      )
    }
    UNION
    # RCE
    {
      BIND(
        <https://services.rce.geovoorziening.nl/buitenplaatsen/wms?request=GetCapabilities>
        AS ?mapEndpoint
      )
    }
  }
}
