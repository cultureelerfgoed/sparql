#+ name: RM-kennisdata-register-1
#+ description: Inhoudelijke data (kennis) in het rijksmonumentenregister: kopjes Bouwactiviteiten, Bouwstijlen, Ambachten
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

construct {
  ?rijksmonument a ceo:Rijksmonument;
    ceo:rijksmonumentnummer ?rijksmonumentnummer;
    ceo:datumInschrijvingInMonumentenregister ?datumInschrijving;
    ceo:heeftGebeurtenis ?gebeurtenis;
    ceo:heeftType ?typeResource;
    ceo:heeftStijlEnCultuur ?stijl;
    ceo:heeftOorspronkelijkeFunctie ?functieResource;
    ceo:heeftKennisregistratie ?kennisregistratie.

  ?gebeurtenis a ceo:Gebeurtenis;
    ceo:heeftGebeurtenisNaam ?gebeurtenisConcept;
    ceo:heeftDatering ?datering;
    ceo:heeftActorEnRol ?actorenrol;
    ceo:kennisregistratieToelichting ?gebeurtenisToelichting.

  ?gebeurtenisConcept skos:prefLabel ?gebeurtenisLabel;
                      skos:exactMatch ?gebeurtenisMatch.

  ?datering a ceo:Datering;
    ceo:heeftIndicatieNauwkeurigheid ?nauwkeurigheidConcept;
    ceo:heeftBeginDatering ?beginDatering;
    ceo:heeftEindDatering ?eindDatering.

  ?nauwkeurigheidConcept skos:prefLabel ?nauwkeurigheidLabel.
  ?beginDatering a ceo:BeginDatering; ceo:datum ?beginDatum.
  ?eindDatering a ceo:EindDatering; ceo:datum ?eindDatum.

  ?actorenrol ceo:heeftActor ?actorLiteral;
              ceo:heeftRol ?rol;
              ceo:heeftNaam ?actorNaam;
              ceo:heeftPlaats ?actorPlaats;
              ceo:heeftExacteMatch ?actorMatchUri.

  ?typeResource ceo:heeftTypeNaam ?typeConcept.
  ?typeConcept skos:prefLabel ?typeLabel;
               skos:exactMatch ?typeMatch;
               ceo:kennisregistratieToelichting ?typeToelichting.

  ?stijl ceo:heeftStijlEnCultuurNaam ?stijlConcept.
  ?stijlConcept skos:prefLabel ?stijlLabel;
                skos:exactMatch ?stijlMatch;
                ceo:kennisregistratieToelichting ?stijlToelichting;
                ceo:heeftZuiverheid ?zuiverheidConcept.

  ?zuiverheidConcept skos:prefLabel ?zuiverheidLabel.

  ?functieResource a ceo:Functie;
    ceo:heeftFunctieNaam ?functieConcept;
    ceo:formeelStandpunt ?standpunt.
  ?functieConcept skos:prefLabel ?functieLabel;
                  skos:exactMatch ?functieMatch.

  ?kennisregistratie ceo:kennisregistratieToelichting ?kennisToelichting.
}
where {
  graph graph:instanties-rce {
    ?rijksmonument a ceo:Rijksmonument;
      ceo:rijksmonumentnummer ?rijksmonumentnummer;
      ceo:datumInschrijvingInMonumentenregister ?datumInschrijving.
  }

  {
    graph graph:instanties-rce {
      ?rijksmonument ceo:heeftGebeurtenis ?gebeurtenis.
      ?gebeurtenis a ceo:Gebeurtenis;
                   ceo:heeftGebeurtenisNaam ?gebeurtenisConcept.
      ?gebeurtenisConcept skos:prefLabel ?gebeurtenisLabel.

      optional {
        ?gebeurtenis ceo:heeftDatering ?datering.
        ?datering a ceo:Datering;
                  ceo:heeftIndicatieNauwkeurigheid ?nauwkeurigheidConcept;
                  ceo:heeftBeginDatering ?beginDatering;
                  ceo:heeftEindDatering ?eindDatering.
        ?beginDatering ceo:datum ?beginDatum.
        ?eindDatering ceo:datum ?eindDatum.
        ?nauwkeurigheidConcept skos:prefLabel ?nauwkeurigheidLabel.
      }

      optional {
        ?gebeurtenis ceo:heeftActorEnRol ?actorenrol.
        ?actorenrol ceo:heeftActor ?actorLiteral;
                    ceo:heeftRol ?rol.

        bind(str(?actorLiteral) as ?actorString)
        bind(strbefore(?actorString, " ;") as ?actorNaam)
        bind(strafter(?actorString, " ;") as ?actorPlaats)

        optional {
          graph graph:actorenrol {
            ?actorUri skos:prefLabel ?actorLiteral.
          }

          service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/Referentienetwerk-1/sparql> {
            optional {
              ?actorUri skos:exactMatch ?actorMatchUri.
              filter strstarts(str(?actorMatchUri), "https://data.rkd.nl/")
            }
          }
        }
      }

      optional {
        ?gebeurtenis ceo:kennisregistratieToelichting ?gebeurtenisToelichting.
      }
    }

    service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/cht/sparql> {
      optional {
        ?gebeurtenisConcept skos:exactMatch ?gebeurtenisMatch.
      }
    }
  }
  union
  {
    graph graph:instanties-rce {
      ?rijksmonument ceo:heeftType ?typeResource.
      ?typeResource ceo:heeftTypeNaam ?typeConcept.
      ?typeConcept skos:prefLabel ?typeLabel.
      optional { ?typeConcept ceo:kennisregistratieToelichting ?typeToelichting. }
    }

    service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/Referentienetwerk-1/sparql> {
      optional {
        ?typeConcept skos:exactMatch ?typeMatch.
      }
    }
  }
  union
  {
    graph graph:instanties-rce {
      ?rijksmonument ceo:heeftStijlEnCultuur ?stijl.
      ?stijl ceo:heeftStijlEnCultuurNaam ?stijlConcept.
      ?stijlConcept skos:prefLabel ?stijlLabel.

      optional {
        ?stijl ceo:heeftZuiverheid ?zuiverheidConcept.
        ?zuiverheidConcept skos:prefLabel ?zuiverheidLabel.
      }

      optional {
        ?stijlConcept ceo:kennisregistratieToelichting ?stijlToelichting.
      }
    }

    service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/Referentienetwerk-1/sparql> {
      optional {
        ?stijlConcept skos:exactMatch ?stijlMatch.
      }
    }
  }
  union
  {
    graph graph:instanties-rce {
      ?rijksmonument ceo:heeftOorspronkelijkeFunctie ?functieResource.
      ?functieResource ceo:heeftFunctieNaam ?functieConcept;
                       ceo:formeelStandpunt ?standpunt.
      ?functieConcept skos:prefLabel ?functieLabel.
    }

    service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/Referentienetwerk-1/sparql> {
      optional {
        ?functieConcept skos:exactMatch ?functieMatch.
      }
    }
  }
  union
  {
    graph graph:instanties-rce {
      ?rijksmonument ceo:heeftKennisregistratie ?kennisregistratie.
      optional {
        ?kennisregistratie ceo:kennisregistratieToelichting ?kennisToelichting.
      }
    }
  }
}
limit 500
