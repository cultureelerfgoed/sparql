#+ name: RM-kennisdata-register-1
#+ description: Inhoudelijke data (kennis) in het rijksmonumentenregister: kopjes Bouwactiviteiten, Bouwstijlen, Ambachten
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

construct {
  ?rijksmonument a ceo:Rijksmonument;
    ceo:rijksmonumentnummer ?rijksmonumentnummer;
    ceo:datumInschrijvingInMonumentenregister ?datumInschrijving;
    ceo:heeftType ?typeResource;
    ceo:heeftGebeurtenis ?gebeurtenis;
    ceo:heeftKennisregistratie ?kennisregistratie;
    ceo:kennisregistratieToelichting ?toelichting.

  ?stijl ceo:heeftStijlEnCultuur ?stijlEnCultuurConcept.
  ?stijlEnCultuurConcept skos:prefLabel ?stijlEnCultuurLabel;
    skos:exactMatch ?stijlEnCultuurMatch;
    ceo:heeftZuiverheid ?zuiverheidConcept.
  ?zuiverheidConcept skos:prefLabel ?zuiverheidLabel.

  ?typeResource ceo:heeftTypeNaam ?typeConcept.
  ?typeConcept skos:prefLabel ?typeLabel;
    skos:exactMatch ?typeMatch.

  ?gebeurtenis ceo:heeftGebeurtenisNaam ?gebeurtenisConcept;
    ceo:heeftDatering ?datering;
    ceo:heeftActorEnRol ?actorenrol.
  ?gebeurtenisConcept skos:prefLabel ?gebeurtenisLabel;
    skos:exactMatch ?gebeurtenisMatch.

  ?datering ceo:heeftIndicatieNauwkeurigheid ?nauwkeurigheidConcept;
    ceo:heeftBeginDatering ?beginDatering;
    ceo:heeftEindDatering ?eindDatering.
  ?nauwkeurigheidConcept skos:prefLabel ?nauwkeurigheidLabel;
    skos:exactMatch ?nauwkeurigheidMatch.

  ?beginDatering ceo:datum ?beginDatum.
  ?eindDatering ceo:datum ?eindDatum.

  ?actorenrol ceo:heeftActor ?actor;
    ceo:heeftRol ?rol.
  ?actor skos:prefLabel ?actorLabel;
    skos:exactMatch ?actorMatch;
    ceo:plaatsActor ?plaatsActor.
}
where {
  graph graph:instanties-rce {
    ?rijksmonument a ceo:Rijksmonument;
      ceo:rijksmonumentnummer ?rijksmonumentnummer;
      ceo:datumInschrijvingInMonumentenregister ?datumInschrijving.

    optional {
      ?rijksmonument ceo:heeftStijlEnCultuur ?stijl.
      ?stijl ceo:heeftStijlEnCultuurNaam ?stijlEnCultuurConcept.
      ?stijlEnCultuurConcept skos:prefLabel ?stijlEnCultuurLabel.

      optional {
        ?stijlEnCultuurConcept ceo:heeftZuiverheid ?zuiverheidConcept.
        ?zuiverheidConcept skos:prefLabel ?zuiverheidLabel.
      }
    }

    optional {
      ?rijksmonument ceo:heeftType ?typeResource.
      ?typeResource ceo:heeftTypeNaam ?typeConcept.
      ?typeConcept skos:prefLabel ?typeLabel.
    }

    optional {
      ?rijksmonument ceo:heeftGebeurtenis ?gebeurtenis.
      ?gebeurtenis ceo:heeftGebeurtenisNaam ?gebeurtenisConcept.
      ?gebeurtenisConcept skos:prefLabel ?gebeurtenisLabel.

      optional {
        ?gebeurtenis ceo:heeftDatering ?datering.
        ?datering a ceo:Datering;
          ceo:heeftIndicatieNauwkeurigheid ?nauwkeurigheidConcept;
          ceo:heeftBeginDatering ?beginDatering;
          ceo:heeftEindDatering ?eindDatering.
        ?nauwkeurigheidConcept skos:prefLabel ?nauwkeurigheidLabel.
      }

      optional {
        ?gebeurtenis ceo:heeftActorEnRol ?actorenrol.
        ?actorenrol ceo:heeftActor ?actor;
          ceo:heeftRol ?rol.
        optional { ?actor skos:prefLabel ?actorLabel. }
        optional { ?actor ceo:plaatsActor ?plaatsActor. }
      }
    }

    optional {
      ?rijksmonument ceo:heeftKennisregistratie ?kennisregistratie.
      ?kennisregistratie ceo:kennisregistratieToelichting ?toelichting.
    }
  }

  # ExactMatch per concept (excl. functie)

  optional {
    service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/cht/sparql> {
      ?typeConcept skos:exactMatch ?typeMatch.
      filter strstarts(str(?typeMatch), "https://data.cultureelerfgoed.nl/term/id/cht/")
      filter not exists {
        ?typeConcept skos:exactMatch ?otherMatch.
        filter(str(?otherMatch) < str(?typeMatch))
        filter strstarts(str(?otherMatch), "https://data.cultureelerfgoed.nl/term/id/cht/")
      }
    }
  }

  optional {
    service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/cht/sparql> {
      ?stijlEnCultuurConcept skos:exactMatch ?stijlEnCultuurMatch.
      filter strstarts(str(?stijlEnCultuurMatch), "https://data.cultureelerfgoed.nl/term/id/cht/")
      filter not exists {
        ?stijlEnCultuurConcept skos:exactMatch ?otherMatch.
        filter(str(?otherMatch) < str(?stijlEnCultuurMatch))
        filter strstarts(str(?otherMatch), "https://data.cultureelerfgoed.nl/term/id/cht/")
      }
    }
  }

  optional {
    service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/cht/sparql> {
      ?gebeurtenisConcept skos:exactMatch ?gebeurtenisMatch.
      filter strstarts(str(?gebeurtenisMatch), "https://data.cultureelerfgoed.nl/term/id/cht/")
      filter not exists {
        ?gebeurtenisConcept skos:exactMatch ?otherMatch.
        filter(str(?otherMatch) < str(?gebeurtenisMatch))
        filter strstarts(str(?otherMatch), "https://data.cultureelerfgoed.nl/term/id/cht/")
      }
    }
  }

  optional {
    service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/cht/sparql> {
      ?nauwkeurigheidConcept skos:exactMatch ?nauwkeurigheidMatch.
      filter strstarts(str(?nauwkeurigheidMatch), "https://data.cultureelerfgoed.nl/term/id/cht/")
      filter not exists {
        ?nauwkeurigheidConcept skos:exactMatch ?otherMatch.
        filter(str(?otherMatch) < str(?nauwkeurigheidMatch))
        filter strstarts(str(?otherMatch), "https://data.cultureelerfgoed.nl/term/id/cht/")
      }
    }
  }

  optional {
    service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/cht/sparql> {
      ?actor skos:exactMatch ?actorMatch.
      filter strstarts(str(?actorMatch), "https://data.rkd.nl/")
      filter not exists {
        ?actor skos:exactMatch ?otherMatch.
        filter(str(?otherMatch) < str(?actorMatch))
        filter strstarts(str(?otherMatch), "https://data.rkd.nl/")
      }
    }
  }
}
limit 500
