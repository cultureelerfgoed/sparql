#+ name: RM-kennisdata-register-1
#+ description: Inhoudelijke data (kennis) in het rijksmonumentenregister: kopjes Bouwactiviteiten, Bouwstijlen, Ambachten
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix rn: <https://data.cultureelerfgoed.nl/term/id/rn/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

construct {
  ?rijksmonument a ceo:Rijksmonument;
    ceo:rijksmonumentnummer ?rijksmonumentnummer;
    ceo:datumInschrijvingInMonumentenregister ?datumInschrijving;
    ceo:heeftOorspronkelijkeFunctie ?functieResource;
    ceo:heeftType ?typeResource;
    ceo:heeftGebeurtenis ?gebeurtenis;
    ceo:heeftKennisregistratie ?kennisregistratie;
    ceo:kennisregistratieToelichting ?toelichting.

  ?functieResource a ceo:Functie;
    ceo:heeftFunctieNaam ?functieConcept;
    ceo:formeelStandpunt ?standpunt.
  ?functieConcept skos:prefLabel ?functieLabel;
                  skos:exactMatch ?functieMatch.
  ?functieMatch skos:prefLabel ?functieMatchLabel.

  ?typeResource ceo:heeftTypeNaam ?typeConcept.
  ?typeConcept skos:prefLabel ?typeLabel;
               skos:exactMatch ?typeMatch.
  ?typeMatch skos:prefLabel ?typeMatchLabel.

  ?gebeurtenis a ceo:Gebeurtenis;
    ceo:heeftGebeurtenisNaam ?gebeurtenisConcept;
    ceo:heeftDatering ?datering.

  ?gebeurtenisConcept skos:prefLabel ?gebeurtenisLabel;
                      skos:exactMatch ?gebeurtenisMatch.
  ?gebeurtenisMatch skos:prefLabel ?gebeurtenisMatchLabel.

  ?datering a ceo:Datering;
    ceo:heeftIndicatieNauwkeurigheid ?nauwkeurigheidConcept;
    ceo:heeftBeginDatering ?beginDatering;
    ceo:heeftEindDatering ?eindDatering.

  ?nauwkeurigheidConcept skos:prefLabel ?nauwkeurigheidLabel;
                         skos:exactMatch ?nauwkeurigheidMatch.
  ?nauwkeurigheidMatch skos:prefLabel ?nauwkeurigheidMatchLabel.

  ?beginDatering a ceo:BeginDatering;
    ceo:datum ?beginDatum.
  ?eindDatering a ceo:EindDatering;
    ceo:datum ?eindDatum.

  ?gebeurtenis ceo:heeftActorEnRol ?actorenrol.
  ?actorenrol ceo:heeftActor ?actor;
              ceo:heeftRol ?rol.

  ?actor rdfs:label ?actorLabel.
  ?actor skos:exactMatch ?actorMatch.
  ?actor ceo:plaatsActor ?plaatsActor.
}
where {
  graph graph:instanties-rce {
    ?rijksmonument a ceo:Rijksmonument;
      ceo:rijksmonumentnummer ?rijksmonumentnummer;
      ceo:datumInschrijvingInMonumentenregister ?datumInschrijving.

    optional {
      ?rijksmonument ceo:heeftOorspronkelijkeFunctie ?functieResource.
      ?functieResource ceo:heeftFunctieNaam ?functieConcept;
                       ceo:formeelStandpunt ?standpunt.
      ?functieConcept skos:prefLabel ?functieLabelRaw.
      bind(replace(?functieLabelRaw, "\\s\\(.*\\)|\\(.*\\)", "") as ?functieLabel)
    }

    optional {
      ?rijksmonument ceo:heeftType ?typeResource.
      ?typeResource ceo:heeftTypeNaam ?typeConcept.
      ?typeConcept skos:prefLabel ?typeLabel.
    }

    optional {
      ?rijksmonument ceo:heeftGebeurtenis ?gebeurtenis.
      ?gebeurtenis a ceo:Gebeurtenis;
                   ceo:heeftGebeurtenisNaam ?gebeurtenisConcept.
      ?gebeurtenisConcept skos:prefLabel ?gebeurtenisLabel.

      optional {
        ?gebeurtenis ceo:heeftDatering ?datering.
        ?datering a ceo:Datering;
                  ceo:heeftIndicatieNauwkeurigheid ?nauwkeurigheidConcept;
                  ceo:heeftBeginDatering ?beginDatering;
                  ceo:heeftEindDatering ?eindDatering.
        ?nauwkeurigheidConcept skos:prefLabel ?nauwkeurigheidLabel.
        ?beginDatering a ceo:BeginDatering;
                       ceo:datum ?beginDatum.
        ?eindDatering a ceo:EindDatering;
                      ceo:datum ?eindDatum.
      }

      optional {
        ?gebeurtenis ceo:heeftActorEnRol ?actorenrol.
        ?actorenrol ceo:heeftActor ?actor;
                    ceo:heeftRol ?rol.

        optional {
          ?actor rdfs:label ?actorLabel.
          optional { ?actor skos:exactMatch ?actorMatch. }
          optional { ?actor ceo:plaatsActor ?plaatsActor. }
        }
      }
    }

    optional {
      ?rijksmonument ceo:heeftKennisregistratie ?kennisregistratie.
      ?kennisregistratie ceo:kennisregistratieToelichting ?toelichting.
    }
  }

  # Alleen externe matches (dus geen rn:2/ structuren)
  service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/referentienetwerk/sparql> {
    optional {
      ?functieConcept skos:exactMatch ?functieMatch.
      filter(strstarts(str(?functieMatch), "http") && !strstarts(str(?functieMatch), str(rn:)))
      ?functieMatch skos:prefLabel ?functieMatchLabel.
    }
    optional {
      ?typeConcept skos:exactMatch ?typeMatch.
      filter(strstarts(str(?typeMatch), "http") && !strstarts(str(?typeMatch), str(rn:)))
      ?typeMatch skos:prefLabel ?typeMatchLabel.
    }
    optional {
      ?gebeurtenisConcept skos:exactMatch ?gebeurtenisMatch.
      filter(strstarts(str(?gebeurtenisMatch), "http") && !strstarts(str(?gebeurtenisMatch), str(rn:)))
      ?gebeurtenisMatch skos:prefLabel ?gebeurtenisMatchLabel.
    }
    optional {
      ?nauwkeurigheidConcept skos:exactMatch ?nauwkeurigheidMatch.
      filter(strstarts(str(?nauwkeurigheidMatch), "http") && !strstarts(str(?nauwkeurigheidMatch), str(rn:)))
      ?nauwkeurigheidMatch skos:prefLabel ?nauwkeurigheidMatchLabel.
    }
  }
}
limit 500
