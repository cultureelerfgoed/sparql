#+ name: RM-kennisdata-register-1
#+ description: Inhoudelijke data (kennis) in het rijksmonumentenregister: kopjes Bouwactiviteiten, Bouwstijlen, Ambachten
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rn: <https://data.cultureelerfgoed.nl/term/id/rn/>

construct {
  ?rijksmonument a ceo:Rijksmonument;
    ceo:rijksmonumentnummer ?rijksmonumentnummer;
    ceo:datumInschrijvingInMonumentenregister ?datumInschrijving;
    ceo:heeftGebeurtenis ?gebeurtenis;
    ceo:heeftKennisregistratie ?kennisregistratie;
    ceo:kennisregistratieToelichting ?toelichting;
    ceo:heeftOorspronkelijkeFunctie ?functies_.

  ?functies_ ceo:heeftFunctieNaam ?functies;
    ceo:formeelStandpunt "1"^^xsd:boolean.
  ?functies skos:prefLabel ?functie;
    skos:exactMatch ?functieMatch.

  ?gebeurtenis ceo:heeftGebeurtenisNaam ?gebeurtenisNaam;
    ceo:heeftDatering ?datering;
    ceo:kennisregistratieToelichting ?gebeurtenisToelichting.
  ?gebeurtenisNaam skos:exactMatch ?gebeurtenisNaamMatch.

  ?datering ceo:heeftIndicatieNauwkeurigheid ?indicatienauwkeurigheid;
    ceo:heeftBeginDatering ?beginDatering;
    ceo:heeftEindDatering ?eindDatering.
  ?indicatienauwkeurigheid skos:exactMatch ?nauwkeurigheidMatch.

  ?beginDatering ceo:datum ?beginDatum.
  ?eindDatering ceo:datum ?eindDatum.

  ?rijksmonument ceo:heeftStijlEnCultuur ?bouwstijl.
  ?bouwstijl ceo:heeftStijlEnCultuurNaam ?bouwstijlLabel;
    ceo:heeftStijlEnCultuurNaam ?bouwstijlUri;
    ceo:heeftZuiverheid ?zuiverheid.
  ?bouwstijlLabel skos:exactMatch ?bouwstijlMatch.
  ?zuiverheid skos:exactMatch ?zuiverheidMatch.

  ?gebeurtenis ceo:heeftActorEnRol ?actorenrol.
  ?actorenrol ceo:heeftActor ?actor;
    ceo:heeftRol ?rol.

  ?actor rdfs:label ?actorLabel;
    skos:exactMatch ?link;
    ceo:plaatsActor ?plaatsActor.
}
where {
  graph graph:instanties-rce {
    ?rijksmonument ceo:rijksmonumentnummer ?rijksmonumentnummer.

    minus {
      ?rijksmonument ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/3e79bb7c-b459-4998-a9ed-78d91d069227>.
    }

    ?rijksmonument ceo:datumInschrijvingInMonumentenregister ?datumInschrijving.

    optional {
      ?rijksmonument ceo:heeftGebeurtenis ?gebeurtenis.
      ?gebeurtenis ceo:heeftGebeurtenisNaam/skos:prefLabel ?gebeurtenisNaam.
    }

    optional {
      ?gebeurtenis ceo:heeftDatering ?datering.
      ?datering ceo:heeftIndicatieNauwkeurigheid/skos:prefLabel ?indicatienauwkeurigheid.
      optional {
        ?datering ceo:heeftBeginDatering ?beginDatering.
        ?beginDatering ceo:datum ?beginDatum.
      }
      optional {
        ?datering ceo:heeftEindDatering ?eindDatering.
        ?eindDatering ceo:datum ?eindDatum.
      }
    }

    optional {
      ?rijksmonument ceo:heeftStijlEnCultuur ?bouwstijl.
      ?bouwstijl ceo:heeftStijlEnCultuurNaam/skos:prefLabel ?bouwstijlLabel.
      ?bouwstijl ceo:heeftStijlEnCultuurNaam ?bouwstijlUri.
      optional { ?bouwstijl ceo:heeftZuiverheid/skos:prefLabel ?zuiverheid. }
    }

    optional { ?gebeurtenis ceo:kennisregistratieToelichting ?gebeurtenisToelichting. }

    optional {
      ?gebeurtenis ceo:heeftActorEnRol ?actorenrol.
      ?actorenrol ceo:heeftActor ?actor.
      optional { ?actor rdfs:label ?actorLabel. }
      optional { ?actor skos:exactMatch ?link. }
      optional { ?actor ceo:plaatsActor ?plaatsActor. }
      optional { ?actorenrol ceo:heeftRol ?rol. }
    }

    optional {
      ?rijksmonument ceo:heeftOorspronkelijkeFunctie ?functies_.
      ?functies_ ceo:heeftFunctieNaam ?functies;
        ceo:formeelStandpunt "1"^^xsd:boolean.
      ?functies skos:prefLabel ?functie_.
      bind(replace(?functie_, "\\s\\(.*\\)|\\(.*\\)", "") as ?functie)
    }

    optional {
      ?rijksmonument ceo:heeftKennisregistratie ?kennisregistratie.
      ?kennisregistratie ceo:kennisregistratieToelichting ?toelichting.
    }
  }

  # SERVICE-block om exactMatch op te halen voor alle relevante labels
  service <https://api.linkeddata.cultureelerfgoed.nl/datasets/thesauri/referentienetwerk/sparql> {
    optional { ?gebeurtenisNaam skos:exactMatch ?gebeurtenisNaamMatch. }
    optional { ?indicatienauwkeurigheid skos:exactMatch ?nauwkeurigheidMatch. }
    optional { ?bouwstijlLabel skos:exactMatch ?bouwstijlMatch. }
    optional { ?zuiverheid skos:exactMatch ?zuiverheidMatch. }
    optional { ?functie skos:exactMatch ?functieMatch. }
  }
}
limit 500
