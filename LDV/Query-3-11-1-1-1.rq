#+ name: Query-3-11-1-1-1
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/Closed-Linked-Data/adviseurs/sparql

PREFIX schema: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX dcterms: <http://purl.org/dc/terms/> 

SELECT ?gemeenteNaam ?vakgebiedNaam ?adviseurNaam (GROUP_CONCAT(DISTINCT ?rijksmonumentnummer; SEPARATOR=", ") AS ?monumentnummers)
WHERE {
  GRAPH <https://linkeddata.cultureelerfgoed.nl/graph/adviseurs-anoniem> {
    # Haal de adviseur op
    ?adviseur a schema:Person ;
              rdfs:label ?adviseurNaam ;
              schema:worksFor ?gemeenteUri ;
              dcterms:subject ?vakgebied .

    # Haal de naam van de gemeente op
    ?gemeenteUri rdfs:label ?gemeenteNaam .

    # Haal de naam van het vakgebied op
    ?vakgebied skos:prefLabel ?vakgebiedNaam .
  }
  
  GRAPH <https://linkeddata.cultureelerfgoed.nl/Closed-Linked-Data/adviseurs/graphs/rijksmonumenten> {
    # Haal monumenten op
    ?rijksmonument a ceo:Rijksmonument ;
                   ceo:rijksmonumentnummer ?rijksmonumentnummer ;
                   ceo:heeftBasisregistratieRelatie ?basisregistratierelatie .
    ?rijksmonument ceo:heeftGemeente ?gemeenteUri .
  }
}
GROUP BY ?gemeenteNaam ?vakgebiedNaam ?adviseurNaam
ORDER BY ?gemeenteNaam ?vakgebiedNaam ?adviseurNaam
LIMIT 1000
