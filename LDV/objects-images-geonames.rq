#+ name: objects-images-geonames
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/colonialobjects/services/colonialobjects/sparql

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sdo: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wgs84: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX bif: <http://www.openlinksw.com/schemas/bif#>
SELECT DISTINCT ?object ?image (SAMPLE(?coordinates_r) AS ?coordinates) ?coordinatesLabel WHERE {
  ?object foaf:depiction ?image;
          sdo:locationCreated/skos:closeMatch ?geoname.
      SERVICE <http://factforge.net/repositories/ff-news> {
        ?geoname 
          wgs84:lat ?lat;
          wgs84:long ?long.
        FILTER (datatype(?lat) = xsd:float) 
        FILTER (datatype(?long) = xsd:float) 
      }
  bind(bif:st_point(?long , ?lat) as ?coordinates_r) 
  bind('<a href="{{object}}"><img src="{{image}}" width=100></a>'^^rdf:HTML as ?coordinatesLabel)
}