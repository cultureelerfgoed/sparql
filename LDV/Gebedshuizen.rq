#+ name: Gebedshuizen
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT distinct ?rijksmonumentnummer ?adres ?woonplaats ?denominatie ?huidigeDenominatie ?gebeurtenis ?actor ?rol ?jaar ?periode 
WHERE {
  GRAPH  graph:gebedshuizen { 
    ?rm_a owl:sameAs ?rm_b ;
          ceox:heeftDenominatie ?den .
    ?den ceox:heeftDenominatieNaam/skos:prefLabel ?denominatie .
    OPTIONAL { ?den ceox:oorspronkelijkeDenominatie ?oorspronkelijkeDenominatie } .
    ?den ceox:huidigeDenominatie ?huidigeDenominatie .
    OPTIONAL {?rm_a ceox:kerkhof ?kerkhof_aanwezig } . 
    OPTIONAL {?rm_a ceox:heeftGebeurtenis ?geb . 
      ?geb ceox:heeftGebeurtenisNaam/skos:prefLabel ?gebeurtenis  } .
    OPTIONAL {?geb ceox:heeftActorEnRol ?actorEnRol . 
      ?actorEnRol ceox:actor ?actor .
      ?actorEnRol ceox:heeftRol ?rolUri .
      ?rolUri skos:prefLabel ?rol} .
    OPTIONAL { ?geb ceox:heeftDatering/ceox:periode ?periode} .
    OPTIONAL { ?geb ceox:heeftDatering/ceox:jaar ?jaar}
  } 
  ?rm_b ceo:rijksmonumentnummer ?rijksmonumentnummer . 
  OPTIONAL { ?rm_b  ceo:heeftOorspronkelijkeFunctie/ceo:heeftFunctieNaam/skos:prefLabel ?functie } . 
  OPTIONAL { ?rm_b ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:volledigAdres ?adres } . 
  OPTIONAL { ?rm_b ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:woonplaatsnaam ?woonplaats }
} 
order by ?rijksmonumentnummer
limit 100