#+ name: Query-11-2-1
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix bgt: <http://bgt.basisregistraties.overheid.nl/def/bgt#>
prefix sor: <https://data.kkg.kadaster.nl/sor/model/def/>
prefix nen3610: <https://data.kkg.kadaster.nl/nen3610/model/def/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix geo: <http://www.opengis.net/ont/geosparql#>

select ?rijksmonumentnummer ?straatnaam ?huisnummer ?postcode ?woonplaatsnaam ?verblijfsobjectIdentificatie ?bagstraat ?baghuisnummer ?bagpostcode ?bagwoonplaatsnaam ?cho ?pandURI  ?verblijfsobjectURI
where {
  graph graph:instanties-rce {
    ?cho a ceo:Rijksmonument.
    ?cho ceo:rijksmonumentnummer ?rijksmonumentnummer.
    ?cho ceo:heeftBasisregistratieRelatie ?basisregistratie.
    ?basisregistratie ceo:heeftBAGRelatie ?bagrelatie.
    ?bagrelatie ceo:heeftPand ?pand.
    ?bagrelatie ceo:heeftVerblijfsobject ?verblijfsobject.
    ?bagrelatie ceo:verblijfsobjectIdentificatie ?verblijfsobjectIdentificatie.
    ?cho ceo:heeftGeometrie ?geometrie .
    ?geometrie geo:asWKT ?shape .
    bind(iri(concat("http://bag.basisregistraties.overheid.nl/bag/id/pand/", ?pand)) as ?pandURI)
    bind(iri(concat("http://bag.basisregistraties.overheid.nl/bag/id/verblijfsobject/", ?verblijfsobjectIdentificatie)) as ?verblijfsobjectURI)

    optional {
      ?bagrelatie ceo:openbareRuimte ?straatnaam.
      ?bagrelatie ceo:huisnummer ?huisnummer.
      ?bagrelatie ceo:postcode ?postcode.
      ?bagrelatie ceo:woonplaatsnaam ?woonplaatsnaam.
    }
  }

  service <https://api.labs.kadaster.nl/datasets/dst/kkg/sparql> {
    # GRAPH <https://data.kkg.kadaster.nl/graph/instanties-adresseerbaarObject> {
    ?verblijfsobject bag:hoofdadres ?hoofdadres .
    ?hoofdadres bag:ligtAan/skos:prefLabel ?bagstraat .
    ?hoofdadres bag:huisnummer ?baghuisnummer .
    ?hoofdadres bag:postcode ?bagpostcode .
    ?hoofdadres bag:ligtAan/bag:ligtIn/skos:prefLabel ?bagwoonplaatsnaam.
  }
    bind(concat('Adres RCE: ', ?straatnaam, ' ', ?huisnummer, " ", ?postcode, " in: ", ?woonplaatsnaam, "en BAGadres: ", ?bagstraat , " " , ?baghuisnummer, " ", ?postcode, " in: ", ?bagwoonplaatsnaam) as ?shapeLabel)
    bind("red" as ?shapeColor)
    bind(15 as ?shapeHeight)
}

