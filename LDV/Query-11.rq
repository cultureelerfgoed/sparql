#+ name: Query-11
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/Rijksmonumenten-sdo/services/Rijksmonumenten-sdo-jena/sparql

PREFIX text: <http://jena.apache.org/text#>
PREFIX schema: <https://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

SELECT DISTINCT (URI(CONCAT("https://monumentenregister.cultureelerfgoed.nl/monumenten/", ?rijksmonumentnummer)) AS ?monumentenregister)
    ?provincie
    ?functies
    ?adres
    ?literal
    ?widget
    ?naam
    ?plaats
    ?monumentaard
WHERE {
    (?choi ?sc ?literal) text:query (
        schema:description '(ijzer OR gietijzer OR smeedijzer OR rvs OR corten OR cortenstaal OR roestvast OR roestvrij OR ijzeren OR roest OR welijzer OR staal OR stalen)' 
        'highlight:s:<strong class="hiLite">|e:</strong>|f:[...]<br>- ').
     (?choi) text:query (schema:addressRegion ?provincie).


    ?choi schema:description ?omschrijving.
    ?choi schema:identifier ?rijksmonumentnummer.

    OPTIONAL {
        ?choi schema:additionalType ?functie.
        BIND(REPLACE(?functie, "\\s\\(.*\\)|\\(.*\\)", "") AS ?functies)
    }

    OPTIONAL {
        ?choi schema:address ?adres.
    }

    ?choi schema:addressLocality ?plaats.
 #   ?choi schema:addressRegion ?provincie.
    ?choi schema:category ?monumentaard.

    OPTIONAL {
        ?choi schema:name ?naam.
    }

    FILTER(!REGEX(?omschrijving, "Corten, J.P.|J.P. Corten|Cortenbach|Cortensstraat|Huis Corten", "i"))

  BIND(STRDT(
  CONCAT(
    '<p><b>Rijksmonument: </b><a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/', STR(?rijksmonumentnummer), '" target="_blank">', STR(?rijksmonumentnummer), '</a></p>',
    '<p>monumentaard: ', COALESCE(STR(?monumentaard), '-'), '</p>',
    '<p>naam: ', COALESCE(STR(?naam), '-'), '</p>',
    '<p>functie: ', COALESCE(STR(?functies), '-'), '</p>',
    '<p>adres: ', COALESCE(STR(?adres), '-'), '</p>',
    '<p>plaats: ', COALESCE(STR(?plaats), '-'), '</p>',
    '<p>provincie: ', COALESCE(STR(?provincie), '-'), '</p>',
    '<p>gevonden woorden in omschrijving:</br>- ', STR(?literal), '</p>'
  ),
  rdf:HTML) AS ?widget)
    }

LIMIT 100
