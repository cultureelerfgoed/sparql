#+ name: Query-6-1-1-1-3
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rn: <https://data.cultureelerfgoed.nl/term/id/rn/>
prefix rnce: <https://data.cultureelerfgoed.nl/id/rnce#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select ?shape (sample(?shapeLabel0) as ?shapeLabel) (sample(?shapeColor0) as ?shapeColor) 
  {
  graph graph:instanties-rce {
    ?rijksmonument
      ceo:datumInschrijvingInMonumentenregister ?datumInschrijving;
      ceo:heeftOorspronkelijkeFunctie ?functie;
      ceo:rijksmonumentnummer ?rijksmonumentnummer;
      ceo:heeftBasisregistratieRelatie/ceo:heeftProvincie ?prov.
   ?rijksmonument ceo:heeftJuridischeStatus rn:b2d9a59a-fe1e-4552-9a05-3c2acddff864.
      optional { ?rijksmonument ceo:heeftGeometrie/geo:asWKT ?shape. }
    ?functie ceo:heeftFunctieNaam ?uri.
    bind(year(xsd:dateTime(?datumInschrijving)) as ?jaarInschrijving)
    filter(?jaarInschrijving >= ?beginJaarInschrijving && ?jaarInschrijving <= ?eindJaarInschrijving)
    bind(concat(str(?jaarInschrijving)) as ?jaarInschrijvings)
    bind(concat(str(?beginJaarInschrijving)," - ", ?eindJaarInschrijving) AS ?periode)
  }
  graph graph:owms {
    ?prov skos:prefLabel ?provincie.
  }  
GRAPH graph:bebouwdeomgeving {
  rn:25fac0f1-77a2-4587-ab04-dfcb66959dd8 skos:narrower+ ?uri .
  ?uri skos:prefLabel ?uriSub .
  BIND(REPLACE(?uriSub, "\\s\\(.*\\)|\\(.*\\)", "") AS ?uriSubs)
}
SERVICE <https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/denominatie-gebedshuizen/sparql> {
  ?rijksmonument ceox:heeftDenominatie/ceox:heeftDenominatieNaam ?den.
  ?den skos:prefLabel ?denominatie;
       rnce:hasChurchMovement/skos:prefLabel ?stroming.
  BIND(str(?denominatie) AS ?denLabel)
}

BIND(strdt(concat(
  STR(?uriSubs), " - ",
  STR(?denominatie), " - ",
  STR(?jaarInschrijving), " - rijksmonument ",
  '<a href="https://monumentenregister.cultureelerfgoed.nl/monumenten/',
  STR(?rijksmonumentnummer), '" target="_blank">',
  STR(?rijksmonumentnummer), '</a>'
),rdf:HTML) as ?shapeLabel0)


  bind(concat(str(?uriSubs), " ", ?periode) as ?subFunctie)
  bind(if(?stroming="Christendom, Evangelisch", "red",
       if(?stroming="Christendom, Katholiek", "purple",
       if(?stroming="Christendom, Oecumenisch", "maroon",
       if(?stroming="Christendom, Overig", "black",
       if(?stroming="Christendom, Protestants", "orange",
       if(?stroming="Christendom, Reformatorisch", "gray",
       if(?stroming="Christendom, Vrijzinnig", "white",
       if(?stroming="Jodendom", "green", "fuchsia")))))))) as ?shapeColor0)
}
group by ?shape
