#+ name: Query-26-1-2
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo:   <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix skos:  <http://www.w3.org/2004/02/skos/core#>
prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFix rn: <https://data.cultureelerfgoed.nl/term/id/rn/2/>

select ?eeuw ?label (count(distinct ?rijksmonument) as ?aantal)
where {
  graph graph:instanties-rce {
    ?rijksmonument a ceo:Rijksmonument ;
        ceo:heeftOorspronkelijkeFunctie ?functie ;
        ceo:heeftOmschrijving/ceo:omschrijving ?desc .

    optional {
      ?rijksmonument ceo:heeftGebeurtenis ?gebeurtenis .
      ?gebeurtenis ceo:heeftDatering ?datering .
      ?datering ceo:heeftBeginDatering/ceo:datum ?beginDatum .
      ?datering ceo:heeftEindDatering/ceo:datum ?eindDatum .
    }
    ?functie ceo:heeftFunctieNaam ?uri .

    # ---- BEPALEN VAN EEUW ALS LABEL (zelfde als query 4) ----
    bind(
      if(bound(?beginDatum) && bound(?eindDatum) &&
         year(xsd:dateTime(?beginDatum)) >= 1200 &&
         year(xsd:dateTime(?eindDatum)) <= 1800,
        concat(str(floor((year(xsd:dateTime(?beginDatum))) / 100) + 1), "e eeuw"),
        if(regex(lcase(str(?desc)), "13e eeuw|xiii"), "13e eeuw",
        if(regex(lcase(str(?desc)), "14e eeuw|xiv"), "14e eeuw",
        if(regex(lcase(str(?desc)), "15e eeuw|xv"), "15e eeuw",
        if(regex(lcase(str(?desc)), "16e eeuw|xvi"), "16e eeuw",
        if(regex(lcase(str(?desc)), "17e eeuw|xvii"), "17e eeuw",
        if(regex(lcase(str(?desc)), "18e eeuw|xviii"), "18e eeuw", "")))))))
       as ?eeuw
    )

    filter(?eeuw in ("13e eeuw","14e eeuw","15e eeuw","16e eeuw","17e eeuw","18e eeuw"))

    # ---- FILTER OP HOUTEN KAPCONSTRUCTIES (zelfde als query 4) ----
    FILTER(
      regex(lcase(str(?desc)),
        "\\bkap\\b|houten kap|kapconstructie|gebint(en)?|spantenkap|beuken kap|sporen?kap|han[ea]balk|gording(en)?|spant(en)?|kappen|dakstoel|kapvoet|eiken kap|vur(e|en)houten kap|fliering|stijlenkap|nok(balk|stijl)?|koningsstijl?|koningsspant|ankerbalk(spant)?|schaarspant|halfspant|keper|hoekkeper|spantbeen|trekbalk|juk|kapjuk|korbeel|stijl(en)? en regelwerk"
      )
    )

    # ---- UITSLUITING 19e EEUW EN NEOSTIJLEN (zelfde als query 4) ----
    filter(
      !regex(str(?desc), "\\b(18[1-9][0-9]|19[0-9]{2}|20[0-9]{2}|21[0-9]{2})\\b") &&
      !regex(lcase(str(?desc)), "\\b(19e|20e|21e) eeuw\\b") &&
      !regex(str(?desc), "\\bXIX\\b|\\bXX\\b|\\bXXI\\b") &&
      !regex(lcase(str(?desc)), "neo\\s?(got|romaans|renaissance|barok|classic)")
    )
  }

  # ---- KOPPELING NAAR HOOFDCATEGORIE ----
  graph graph:bebouwdeomgeving {
    values (?label ?narrow) {
      ("Archeologie (N)" <https://data.cultureelerfgoed.nl/term/id/rn/2/d60159d2-8b55-47b7-8301-5ac82b0f2d7f>)
      ("Bestuursgebouwen, rechtsgebouwen en overheidsgebouwen" <https://data.cultureelerfgoed.nl/term/id/rn/2/74a847b5-1e0f-4f66-b910-90d2c8d9fa04>)
      ("Boerderijen, molens en bedrijven" <https://data.cultureelerfgoed.nl/term/id/rn/2/b8077035-db8f-47f1-ae1d-e64f75344fcf>)
      ("Cultuur, gezondheid en wetenschap" <https://data.cultureelerfgoed.nl/term/id/rn/2/0be0a6c9-0738-41cc-aaac-550d258c4261>)
      ("Handelsgebouwen, opslag- en transportgebouwen" <https://data.cultureelerfgoed.nl/term/id/rn/2/e88ccbf4-e41d-49bf-9876-0f71db0e6646>)
      ("Kastelen, landhuizen en parken" <https://data.cultureelerfgoed.nl/term/id/rn/2/b2511baf-3b70-4667-98dd-1b850c7ea53f>)
      ("Religieuze gebouwen" <https://data.cultureelerfgoed.nl/term/id/rn/2/25fac0f1-77a2-4587-ab04-dfcb66959dd8>)
      ("Sport, recreatie, vereniging en horeca" <https://data.cultureelerfgoed.nl/term/id/rn/2/b797b89c-1e0a-4ce7-869b-817cd98259b0>)
      ("Uitvaartcentra en begraafplaatsen" <https://data.cultureelerfgoed.nl/term/id/rn/2/1680dfc0-666a-4a01-9781-59e9af26ec51>)
      ("Verdedigingswerken en militaire gebouwen" <https://data.cultureelerfgoed.nl/term/id/rn/2/5013dcbc-1090-42e9-bc22-92de47e43783>)
      ("Voorwerpen op pleinen en dergelijke" <https://data.cultureelerfgoed.nl/term/id/rn/2/92cda3e4-8c6a-41dc-9a81-02f8aba88b25>)
      ("Weg- en waterbouwkundige werken" <https://data.cultureelerfgoed.nl/term/id/rn/2/11c897ed-d35e-4191-9254-7ab95d9d63bc>)
      ("Woningen en woningbouwcomplexen" <https://data.cultureelerfgoed.nl/term/id/rn/2/5b7dd16c-fa8d-4d68-984a-9ec0efc650d4>)
    }
    ?narrow skos:narrower+ ?uri .
  }
}
group by ?eeuw ?label
order by ?eeuw ?label
