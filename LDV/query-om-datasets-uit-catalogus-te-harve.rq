#+ name: query-om-datasets-uit-catalogus-te-harve
#+ description: SPARQL-query om datasets uit de catalogus te harvesten
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/datacatalog/services/datacalatog/sparql

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct:  <http://purl.org/dc/terms/>

SELECT ?dataset ?title ?description ?issued ?modified ?accessURL ?format ?license ?sparqlEndpoint ?landingPage
WHERE {
  GRAPH <https://linkeddata.cultureelerfgoed.nl/catalog> {
    <https://linkeddata.cultureelerfgoed.nl/catalog> dcat:dataset ?dataset .
  }
  GRAPH ?dataset {
    ?dataset a dcat:Dataset ;
             dct:title ?title ;
             dct:description ?description ;
             dct:issued ?issued .

    OPTIONAL { ?dataset dct:modified ?modified . }
    OPTIONAL { ?dataset dcat:landingPage ?landingPage . }

    OPTIONAL {
      ?dataset dcat:distribution ?dist .
      ?dist dcat:accessURL ?accessURL .
      OPTIONAL { ?dist dct:format ?format . }
      OPTIONAL { ?dist dct:license ?license . }
    }

    OPTIONAL {
      ?dataset dcat:service ?svc .
      ?svc dcat:endpointURL ?sparqlEndpoint .
    }
  }
}
