#+ name: query-om-datasets-uit-catalogus-te-harve
#+ description: SPARQL-query om datasets uit de catalogus te harvesten
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/datacatalog/services/datacalatog/sparql

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct:  <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

SELECT ?dataset ?title ?description ?issued ?modified ?accessURL ?format ?license
       ?theme ?keyword ?publisher ?creatorName ?contactName ?contactMail ?landingPage
WHERE {
  GRAPH <https://linkeddata.cultureelerfgoed.nl/catalog> {
    <https://linkeddata.cultureelerfgoed.nl/catalog> dcat:dataset ?dataset .
  }

  GRAPH ?dataset {
    ?dataset a dcat:Dataset ;
             dct:title ?title ;
             dct:description ?description ;
             dct:issued ?issued .

    OPTIONAL { ?dataset dct:modified ?modified . }
    OPTIONAL { ?dataset dct:publisher ?publisher . }
    OPTIONAL { ?dataset dcat:theme ?theme . }
    OPTIONAL { ?dataset dcat:keyword ?keyword . }
    OPTIONAL { ?dataset dcat:landingPage ?landingPage . }

    OPTIONAL {
      ?dataset dcat:creator ?creator .
      ?creator foaf:name ?creatorName .
    }

    OPTIONAL {
      ?dataset dcat:contactPoint ?cp .
      ?cp vcard:fn ?contactName .
      OPTIONAL { ?cp vcard:hasEmail ?contactMail . }
    }

    OPTIONAL {
      ?dataset dcat:distribution ?dist .
      ?dist dcat:accessURL ?accessURL .
      OPTIONAL { ?dist dct:format ?format . }
      OPTIONAL { ?dist dct:license ?license . }
    }
  }
}
