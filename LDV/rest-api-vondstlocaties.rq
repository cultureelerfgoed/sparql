#+ name: rest-api-vondstlocaties
#+ description: REST API query for the path vondstlocaties.
#- 
#- Due to the time-out issue, levels 6 and 7 of nesting from line 78 to 83 were commented out.
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
construct {
  $this ?p1 ?o1.
  ?o1 ?p2 ?o2.
  ?o2 ?p3 ?o3.
  ?o3 ?p4 ?o4.
  ?o4 ?p5 ?o5.
#  ?o5 ?p6 ?o6.
#  ?o6 ?p7 ?o7.
} where {
  graph graph:instanties-rce {
    {
      #! paginate: true
      select distinct $this {
        $this
          a ceo:Vondstlocatie;
          ceo:cultuurhistorischObjectnummer ?cultuurhistorischObjectnummer.

        bind(?archeologischeComplexen as ?archeologischeComplexen1)
        optional {
          $this ceo:ligtInObject ?archeologischeComplexen2.
          ?archeologischeComplexen2 a ceo:ArcheologischComplex.
        }
        filter(!bound(?archeologischeComplexen1) || ?archeologischeComplexen1 = ?archeologischeComplexen2)

        bind(?archeologischeOnderzoeksgebieden as ?archeologischeOnderzoeksgebieden1)
        optional {
          $this ceo:ligtInObject ?archeologischeOnderzoeksgebieden2.
          ?archeologischeOnderzoeksgebieden2 a ceo:ArcheologischOnderzoeksgebied.
        }
        filter(!bound(?archeologischeOnderzoeksgebieden1) || ?archeologischeOnderzoeksgebieden1 = ?archeologischeOnderzoeksgebieden2)

        bind(?archis2Vondstmeldingsnummer as ?archis2Vondstmeldingsnummer1)
        optional {
          $this ceo:archis2Vondstmeldingsnummer ?archis2Vondstmeldingsnummer2.
        }
        filter(!bound(?archis2Vondstmeldingsnummer1) || ?archis2Vondstmeldingsnummer1 = ?archis2Vondstmeldingsnummer2)

        bind(?archis2Waarnemingsnummer as ?archis2Waarnemingsnummer1)
        optional {
          $this ceo:archis2Waarnemingsnummer ?archis2Waarnemingsnummer2.
        }
        filter(!bound(?archis2Waarnemingsnummer1) || ?archis2Waarnemingsnummer1 = ?archis2Waarnemingsnummer2)

        bind(?grondsporen as ?grondsporen1)
        optional {
          $this ceo:bevatObject ?grondsporen2.
          ?grondsporen2 a ceo:Grondsporen.
        }
        filter(!bound(?grondsporen1) || ?grondsporen1 = ?grondsporen2)

        bind(?vondsten as ?vondsten1)
        optional {
          $this ceo:bevatObject ?vondsten2.
          ?vondsten2 a ceo:Vondsten.
        }
        filter(!bound(?vondsten1) || ?vondsten1 = ?vondsten2)

        bind(?woonplaatsnaam as ?woonplaatsnaam1)
        optional {
          $this ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:woonplaatsnaam ?woonplaatsnaam2.
        }
        filter(!bound(?woonplaatsnaam1) || ?woonplaatsnaam1 = ?woonplaatsnaam2)
      }
      limit 5
      # paginate
    }
    $this ?p1 ?o1.
    optional {
      filter(?p1 not in (ceo:bevatObject, ceo:isHoofdobjectVanComplex, ceo:isOnderdeelVanComplex))
      ?o1 ?p2 ?o2.
      optional {
        ?o2 ?p3 ?o3.
        optional {
          ?o3 ?p4 ?o4.
          optional {
            ?o4 ?p5 ?o5.
 #                       optional {
  #                        ?o5 ?p6 ?o6.
            #              optional {
            #                ?o6 ?p7 ?o7.
            #              }
   #                     }
          }
       }
      }
    }
  }
}
