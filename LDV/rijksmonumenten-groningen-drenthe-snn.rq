#+ name: rijksmonumenten-groningen-drenthe-snn
#+ description: Groningen en Noord-Drenthe
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix ceox: <https://linkeddata.cultureelerfgoed.nl/def/ceox#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rn: <https://data.cultureelerfgoed.nl/term/id/rn/>
prefix owms: <https://linkeddata.cultureelerfgoed.nl/graph/owms>
prefix geo: <http://www.opengis.net/ont/geosparql#>

select (iri(concat("https://monumentenregister.cultureelerfgoed.nl/monumenten/", ?rijksmonumentnummer)) as ?monumentenregister)
  ?rijksmonumentnummer ?naam ?datumInschrijving ?adres ?provincie ?gemeente ?postcode ?woonplaatsnaam
  ?functieLabel ?omschrijving ?pand ?perceel ?sectie ?verblijfsobject ?geometrie
where {
  graph graph:instanties-rce {
    ?rijksmonument ceo:rijksmonumentnummer ?rijksmonumentnummer.
    ?rijksmonument ceo:heeftJuridischeStatus <https://data.cultureelerfgoed.nl/term/id/rn/b2d9a59a-fe1e-4552-9a05-3c2acddff864>.
    ?rijksmonument ceo:heeftMonumentAard <https://data.cultureelerfgoed.nl/term/id/rn/fc966a68-8863-4970-a83e-110f96006c21>.
    ?rijksmonument ceo:datumInschrijvingInMonumentenregister ?datumInschrijving.
    optional {
      ?rijksmonument ceo:heeftNaam/ceo:naam ?naam.
    }
  }

  # Subquery voor omschrijving
  {
    select ?rijksmonument ?omschrijving
    where {
      graph graph:instanties-rce {
        optional {
          ?rijksmonument ceo:heeftOmschrijving ?omschrijvingStmt.
          ?omschrijvingStmt ceo:omschrijving ?omschrijving.
          ?omschrijvingStmt ceo:formeelStandpunt true.
        }
      }
    }
  }

  # Subquery voor BAG-relatie
  {
    select ?rijksmonument ?straat ?huisnummer ?adres ?pand ?verblijfsobject ?prov ?gem ?postcode ?woonplaatsnaam
    where {
      graph graph:instanties-rce {
        ?rijksmonument ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie ?bagrelatie.
        ?rijksmonument ceo:heeftBasisregistratieRelatie/ceo:heeftProvincie ?prov.
        ?rijksmonument ceo:heeftBasisregistratieRelatie/ceo:heeftGemeente ?gem.
        ?rijksmonument ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:postcode ?postcode_.
        ?bagrelatie ceo:volledigAdres ?adres.

      #  bind(replace(?postcode_, "([0-9]{4})([A-Z]{2})", "$1 $2") as ?postcode)
        ?rijksmonument ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:woonplaatsnaam ?woonplaatsnaam.
        optional {
          ?bagrelatie ceo:heeftVerblijfsobject ?verblijfsobject.
        }
        optional {
          ?bagrelatie ceo:heeftPand ?pand.
        }
      }
    }
  }

  # Subquery voor BRK-relatie
  {
    select ?rijksmonument ?perceel ?sectie
    where {
      graph graph:instanties-rce {
        ?rijksmonument ceo:heeftBasisregistratieRelatie/ceo:heeftBRKRelatie ?brkrelatie.
        optional {
          ?brkrelatie ceo:perceelnummer ?perceel.
        }
        optional {
          ?brkrelatie ceo:sectie ?sectie.
        }
      }
    }
  }
  # Subquery voor geometrie
  {
    select ?rijksmonument ?geometrie
    where {
      graph graph:instanties-rce {
        ?rijksmonument ceo:heeftGeometrie/geo:asWKT ?geometrie.
      }
    }
  }
  # Subquery voor functie
  {
    select ?rijksmonument ?functieLabel
    where {
      graph graph:instanties-rce {
        ?rijksmonument ceo:heeftOorspronkelijkeFunctie ?functies_.
        ?functies_ ceo:heeftFunctieNaam ?functies.
        ?functies skos:prefLabel ?functie.
        bind(replace(?functie, "\\s\\(.*\\)|\\(.*\\)", "") as ?functieLabel)
      }
    }
  }

  # Provincie en gemeente via OWMS graph
  graph owms: {
    ?prov skos:prefLabel ?provincie.
    ?gem skos:prefLabel ?gemeente.
  }
}

group by ?rijksmonument ?monumentenregister ?rijksmonumentnummer ?naam ?datumInschrijving ?adres ?provincie ?gemeente ?postcode ?woonplaatsnaam
  ?functieLabel ?omschrijving ?pand ?perceel ?sectie ?verblijfsobject ?geometrie
order by ?rijksmonumentnummer
