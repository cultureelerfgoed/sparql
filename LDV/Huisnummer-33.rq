#+ name: Huisnummer-33
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/sparql

prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
prefix ceo:   <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix skos:  <http://www.w3.org/2004/02/skos/core#>
prefix rn:    <https://data.cultureelerfgoed.nl/term/id/rn/>

select
  (iri(concat("https://monumentenregister.cultureelerfgoed.nl/monumenten/", ?rijksmonumentnummer)) as ?monumentenregister) 
  ?rijksmonumentnummer
  (sample(?adres)          as ?adres1)
  (sample(?postcode)       as ?postcode1)
  (sample(?woonplaatsnaam) as ?woonplaatsnaam1)
  #(sample(?huisnummer)     as ?huisnummer1)
  (sample(?omschrijving) as ?omschrijving1)
  (group_concat(distinct ?functie; separator=", ") as ?functiesSamengevoegd1)
where {

  graph graph:instanties-rce {
    ?rijksmonument
      ceo:rijksmonumentnummer ?rijksmonumentnummer ;
      ceo:datumInschrijvingInMonumentenregister ?datumInschrijving ;
      ceo:heeftJuridischeStatus rn:b2d9a59a-fe1e-4552-9a05-3c2acddff864 ;
      ceo:heeftMonumentAard    rn:fc966a68-8863-4970-a83e-110f96006c21 ;
      ceo:heeftBasisregistratieRelatie ?basisrel ;
      ceo:heeftOorspronkelijkeFunctie ?standpunt_functie ;
      ceo:heeftOmschrijving/ceo:heeftOmschrijvingstype rn:0273a12d-7259-4391-aee2-5dac264aebe9 ;
      ceo:heeftOmschrijving/ceo:omschrijving ?omschrijving.
     
      
    ?standpunt_functie
      ceo:hoofdfunctie true ;
      ceo:heeftFunctieNaam ?functiesIRI .

    ?basisrel ceo:heeftBAGRelatie ?bagrelatie .

    optional { ?bagrelatie ceo:volledigAdres    ?adres         . }
    optional { ?bagrelatie ceo:postcode         ?postcode_     . }
    optional { ?bagrelatie ceo:woonplaatsnaam   ?woonplaatsnaam. }
    optional { ?bagrelatie ceo:openbareRuimte   ?straat        . }
    optional { ?bagrelatie ceo:huisnummer       ?huisnummer    . }

    bind(replace(str(?postcode_), "([0-9]{4})([A-Z]{2})", "$1 $2") as ?postcode)
  }

  graph graph:bebouwdeomgeving {
    ?functiesIRI skos:prefLabel ?functieRaw .
    bind(replace(?functieRaw, "\\s\\(.*\\)|\\(.*\\)", "") as ?functie)
  }

  filter(regex(lcase(str(?huisnummer)), "^33(?![0-9])", "i"))
}
group by ?rijksmonumentnummer ?monumentenregister

