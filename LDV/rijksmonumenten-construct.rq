#+ name: rijksmonumenten-construct
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
construct {
  ?adresgegevens
    a ceo:Adresgegevens;
    ceo:gemeente ?gemeente;
    ceo:gemeenteCode ?gemeentecode;
    ceo:gemeenteOwms ?gemeenteOwms;
    ceo:huisnummer ?huisnummer;
    ceo:openbareRuimte ?openbareRuimte;
    ceo:postcode ?postcode;
    ceo:provincie ?provincie;
    ceo:provincieCode ?provinciecode;
    ceo:provincieOwms ?provincieOwms;
    ceo:woonplaatsnaam ?plaatsnaam.
  ?functie
    a ceo:Functie;
    ceo:functie ?functieNaamLabel;
    ceo:status ?functieStatus.
  ?geometrie 
    a ceo:Geometrie;
    ceo:herkomst ?geometrieHerkomst;
    ceo:kwaliteit ?geometrieKwaliteit;
    ceo:status ?geometrieStatusLabel;
    ceo:waarde ?geoObject.
  ?geoObject 
    a ?wktType;
    ceo:coordinates ?wkt.
  ?hoofdobjectVanComplex
    a ceo:HoofdobjectVanComplex;
    ceo:href ?hoofdobjectVanComplexUri.
  ?link
    a ceo:_Link;
    ceo:hoofdobjectVanComplex ?hoofdobjectVanComplex;
    ceo:onderdeelVanComplex ?onderdeelVanComplex;
    ceo:self ?self.
  ?locatieAanduiding
    a ceo:LocatieAanduiding;
    ceo:adressen ?adresgegevens;
    ceo:kaartbladcode ?kaartbladcode;
    ceo:locatienaam ?locatienaam;
    ceo:locatieomschrijving ?locatieomschrijving;
    ceo:nummer ?locatieAanduidingnummer;
    ceo:status ?locatieAanduidingStatusLabel.
  ?onderdeelVanComplex
    a ceo:OnderdeelvanComplex;
    ceo:href ?onderdeelVanComplexUri.
  ?rijksmonument
    a ceo:Rijksmonument;
    ceo:_links ?link;
    ceo:bebouwdeKomType ?bebouwdeKomType;
    ceo:cultuurhistorischObjectnummer ?choNummer;
    ceo:functies ?functie;
    ceo:geometrie ?geometrie;
    ceo:juridischeStatus ?juridischeStatusLabel;
    ceo:locatieaanduidingen ?locatieAanduiding;
    ceo:monumentAard ?monumentAard;
    ceo:namen ?naam;
    ceo:omschrijvingen ?omschrijving;
    ceo:rijksmonumentnummer ?rijksmonumentnummer;
    ceo:types ?type.
  ?self
    a ceo:Self;
    ceo:href ?selfUri.
} where {
  ?rijksmonument
    a ceo:Rijksmonument;
    ceo:cultuurhistorischObjectnummer ?choNummer;
    ceo:heeftJuridischeStatus/skos:prefLabel ?juridischeStatusLabel;
    ceo:heeftMonumentAard/skos:prefLabel ?monumentAard;
    ceo:heeftOmschrijving/ceo:omschrijving ?omschrijving;
    ceo:rijksmonumentnummer ?rijksmonumentnummer.
  optional {
    ?rijksmonument ceo:heeftBebouwdeKomType/skos:prefLabel ?bebouwdeKomType.
  }
  optional {
    ?rijksmonument ceo:isOnderdeelVanComplex/ceo:heeftNaam/ceo:naam ?naam.
  }
  optional {
    ?rijksmonument ceo:heeftType/ceo:heeftTypeNaam/skos:prefLabel ?type.
  }
  optional {
    ?heeftFunctie
      rdfs:label ?heeftFunctieLabel;
      rdfs:subPropertyOf ceo:heeftFunctie.
    ?rijksmonument ?heeftFunctie ?functie.
    ?functie ceo:heeftFunctieNaam/skos:prefLabel ?functieNaamLabel.
    bind(str(?heeftFunctieLabel) as ?functieStatus)
  }
  optional {
    ?rijksmonument ceo:heeftLocatieAanduiding ?locatieAanduiding.
    ?locatieAanduiding
      a ceo:LocatieAanduiding;
      ceo:locatieAanduidingnummer ?locatieAanduidingnummer.
    optional {
      ?locatieAanduiding ceo:heeftLocatieAdres ?adresgegevens.
      ?adresgegevens
        a ceo:Adresgegevens;
        ceo:woonplaatsnaam ?plaatsnaam.
      optional {
        ?adresgegevens
          ceo:heeftProvincie ?provincieOwms;
          ceo:huisnummer ?huisnummer;
          ceo:openbareRuimte ?openbareRuimte.
      }
      optional {
        ?adresgegevens ceo:provincieCode ?provinciecode.
      }
      optional {
        ?adresgegevens ceo:gemeenteCode ?gemeentecode.
      }
      optional {
        ?adresgegevens ceo:postcode ?postcode.
      }
      optional {
        ?adresgegevens ceo:heeftGemeente ?gemeenteOwms.
      }
      optional {
        ?adresgegevens ceo:heeftGemeente/rdfs:label ?gemeente.
      }
      optional {
        ?adresgegevens ceo:heeftProvincie/rdfs:label ?provincie.
      } 
    }
    optional {
      ?locatieAanduiding ceo:locatienaam ?locatienaam.
    }
    optional {
      ?locatieAanduiding ceo:kaartbladcode ?kaartbladcode.
    }
    optional {
      ?locatieAanduiding ceo:locatieomschrijving ?locatieomschrijving.
    }
    optional {
      ?locatieAanduiding ceo:heeftLocatieAanduidingStatus/skos:prefLabel ?locatieAanduidingStatusLabel.
    }
  }
  optional {
    ?rijksmonument ceo:heeftGeometrie ?geometrie.
    ?geometrie
      a ceo:Geometrie;
      ceo:heeftGeometrieKwaliteit/skos:prefLabel ?geometrieKwaliteit;
      ceo:heeftGeometrieStatus/skos:prefLabel ?geometrieStatusLabel;
      ceo:heeftHerkomst/skos:prefLabel ?geometrieHerkomst;
      geo:asWKT ?wkt.
  }
}
