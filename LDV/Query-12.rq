#+ name: Query-12
#+ service: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

#+ https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql
# Definieer PREFIXes voor diverse namenruimten die in de query worden gebruikt.
PREFIX graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rn: <https://data.cultureelerfgoed.nl/term/id/rn/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>

# De SELECT-clausule specificeert welke variabelen zullen worden geretourneerd.
# Elk van de geselecteerde variabelen bevat het aantal unieke instanties in de betreffende categorie.
SELECT (COUNT(DISTINCT ?rijksmonument) AS ?Rijksmonumenten)
       (COUNT(DISTINCT ?beschermd) AS ?Beschermd)
       (COUNT(DISTINCT ?gebouwd) AS ?BeschermdGebouwd)
       (COUNT(DISTINCT ?archeologisch) AS ?BeschermdArcheologisch)
       (COUNT(DISTINCT ?nietbeschermd) AS ?NietBeschermd)
       (COUNT(DISTINCT ?voorbeschermd) AS ?VoorBeschermd)
       (COUNT(DISTINCT ?complex) AS ?Complexen)
       (COUNT(DISTINCT ?rmincomplex) AS ?RMinComplexen)
       (COUNT(DISTINCT ?gezicht) AS ?Gezichten)
       (COUNT(DISTINCT ?werelderfgoed) AS ?Werelderfgoed)
       (COUNT(DISTINCT ?vondst) AS ?Vondsten)
       (COUNT(DISTINCT ?grondspoor) AS ?Grondsporen)
       (COUNT(DISTINCT ?vondstlocatie) AS ?Vondstlocaties)
       (COUNT(DISTINCT ?arch_complex) AS ?Arch_Complexen)
       (COUNT(DISTINCT ?arch_terrein) AS ?Terreinen)
       (COUNT(DISTINCT ?onderzoeksgebied) AS ?Onderzoeksgebied)

# De WHERE-clausule specificeert de voorwaarden waaraan de data moeten voldoen.
WHERE {
# De GRAPH-clausule specificeert de graph waarin we geïnteresseerd zijn.
  GRAPH graph:instanties-rce {
# Elk blok binnen deze haakjes specificeert een type instantie dat we willen tellen.
# UNION wordt gebruikt om meerdere types te tellen in dezelfde query.
    { ?rijksmonument a ceo:Rijksmonument. }
     UNION { ?beschermd a ceo:Rijksmonument ; ceo:heeftJuridischeStatus rn:b2d9a59a-fe1e-4552-9a05-3c2acddff864 .}
    UNION { ?gebouwd a ceo:Rijksmonument ; ceo:heeftJuridischeStatus rn:b2d9a59a-fe1e-4552-9a05-3c2acddff864 ; ceo:heeftMonumentAard rn:fc966a68-8863-4970-a83e-110f96006c21.}
    UNION { ?nietbeschermd a ceo:Rijksmonument ; ceo:heeftJuridischeStatus rn:3e79bb7c-b459-4998-a9ed-78d91d069227 .}
    UNION { ?voorbeschermd a ceo:Rijksmonument ; ceo:heeftJuridischeStatus rn:2e93edd1-098f-4f31-ae7e-72cb77f4d2ca .}
    UNION { ?archeologisch a ceo:Rijksmonument ; ceo:heeftJuridischeStatus rn:b2d9a59a-fe1e-4552-9a05-3c2acddff864 ; ceo:heeftMonumentAard rn:b673c8c1-5d93-496d-8f9e-89133d579d77.}
    UNION { ?complex a ceo:Complex. }
    UNION { ?rmincomplex a ceo:Complex. ?rmincomplex ceo:heeftRijksmonument ?rmincomplex_rijksmonument. }
    UNION { ?gezicht a ceo:Gezicht. }
    UNION { ?werelderfgoed a ceo:Werelderfgoed. }
    UNION { ?vondst a ceo:Vondsten. }
    UNION { ?grondspoor a ceo:Grondsporen. }
    UNION { ?vondstlocatie a ceo:Vondstlocatie. }
    UNION { ?arch_complex a ceo:ArcheologischComplex .}
    UNION { ?arch_terrein a ceo:ArcheologischTerrein. }
    UNION { ?onderzoeksgebied a ceo:ArcheologischOnderzoeksgebied. }
  }
}

