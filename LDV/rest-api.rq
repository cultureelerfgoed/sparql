#+ name: rest-api
#+ endpoint: https://api.linkeddata.cultureelerfgoed.nl/datasets/rce/cho/services/cho/sparql

prefix ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>
prefix graph: <https://linkeddata.cultureelerfgoed.nl/graph/>
construct {
  $this ?p1 ?o1.
  ?o1 ?p2 ?o2.
  ?o2 ?p3 ?o3.
  ?o3 ?p4 ?o4.
  ?o4 ?p5 ?o5.
  ?o5 ?p6 ?o6.
  ?o6 ?p7 ?o7.
} where {
  graph graph:instanties-rce {
    {
      select distinct $this {
        $this
          a ceo:Rijksmonument;
          ceo:rijksmonumentnummer ?rijksmonumentnummer.
        optional {
          $this ceo:heeftBasisregistratieRelatie/ceo:heeftBAGRelatie/ceo:woonplaatsnaam ?woonplaatsnaam0.
          filter(coalesce(?woonplaatsnaam) && ?woonplaatsnaam = ?woonplaatsnaam0)
        }
        optional {
          $this ceo:heeftBasisregistratieRelatie/ceo:heeftBRKRelatie/ceo:gemeentecode ?gemeentecode0.
          filter(coalesce(?gemeentecode) && ?gemeentecode = ?gemeentecode0)
        }
        optional {
          $this ceo:heeftBasisregistratieRelatie/ceo:heeftBRKRelatie/ceo:provinciecode ?provinciecode0.
          filter(coalesce(?provinciecode) && ?provinciecode = ?provinciecode0)
        }
      }
      limit 10
      # paginate
    }
    $this ?p1 ?o1.
    optional {
      filter(?p1 not in (ceo:bevatObject, ceo:isHoofdobjectVanComplex, ceo:isOnderdeelVanComplex))
      ?o1 ?p2 ?o2.
      optional {
        ?o2 ?p3 ?o3.
        optional {
          ?o3 ?p4 ?o4.
          optional {
            ?o4 ?p5 ?o5.
            optional {
              ?o5 ?p6 ?o6.
              optional {
                ?o6 ?p7 ?o7.
              }
            }
          }
        }
      }
    }
  }
}
